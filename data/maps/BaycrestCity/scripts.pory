//Baycrest City States
//0: Meet Rival
//1: Rival Defeated
//2: Registered to League

const OBJ_RIVAL = 1

mapscripts BaycrestCity_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
    }
}

script BaycrestCity_Trigger_Rival1 {
    lockall

    applymovement(OBJ_EVENT_ID_PLAYER, ApproachRival1)
    waitmovement(0)
    applymovement(OBJ_RIVAL, LookDown)
    waitmovement(0)

    checkplayergender
    goto_if_eq(VAR_RESULT, MALE, BaycrestCity_Kanami)
    goto_if_eq(VAR_RESULT, FEMALE, BaycrestCity_Shinji)

    applymovement(OBJ_RIVAL, RivalLeave)
    waitmovement(0)
    removeobject(OBJ_RIVAL)

    setvar(VAR_BAYCREST_CITY_STATE, 1)
    setflag(FLAG_HIDE_BAYCREST_CITY_RIVAL)

    releaseall
}

script BaycrestCity_Trigger_Rival2 {
    lockall

    applymovement(OBJ_EVENT_ID_PLAYER, ApproachRival2)
    waitmovement(0)
    applymovement(OBJ_RIVAL, LookDown)
    waitmovement(0)

    checkplayergender
    goto_if_eq(VAR_RESULT, MALE, BaycrestCity_Kanami)
    goto_if_eq(VAR_RESULT, FEMALE, BaycrestCity_Shinji)
}

script BaycrestCity_Kanami {
    playbgm(MUS_ENCOUNTER_MAY, 0)

    special(HealPlayerParty)
    msgbox(BaycrestCity_Text_Rival1F)

    switch(var(VAR_STARTER_MON)) {
        case 0:
            trainerbattle_no_intro(TRAINER_KANAMI_BAYCREST_TURTWIG, BaycrestCity_Text_KanamiDefeated)
            break
        case 1:
            trainerbattle_no_intro(TRAINER_KANAMI_BAYCREST_FENNEKIN, BaycrestCity_Text_KanamiDefeated)
            break
        case 2:
            trainerbattle_no_intro(TRAINER_KANAMI_BAYCREST_QUAXLY, BaycrestCity_Text_KanamiDefeated)
            break
    }

    msgbox(BaycrestCity_Text_Rival2F)
    giveitem(ITEM_TM_SWIFT)
    msgbox(BaycrestCity_Text_Rival3F)
    closemessage
    
    applymovement(OBJ_RIVAL, RivalLeave)
    waitmovement(0)
    removeobject(OBJ_RIVAL)

    setvar(VAR_BAYCREST_CITY_STATE, 1)
    setflag(FLAG_HIDE_BAYCREST_CITY_RIVAL)

    releaseall
}

script BaycrestCity_Shinji {
    playbgm(MUS_ENCOUNTER_BRENDAN, 0)

    special(HealPlayerParty)
    msgbox(BaycrestCity_Text_Rival1M)

    switch(var(VAR_STARTER_MON)) {
        case 0:
            trainerbattle_no_intro(TRAINER_SHINJI_BAYCREST_TURTWIG, BaycrestCity_Text_ShinjiDefeated)
            break
        case 1:
            trainerbattle_no_intro(TRAINER_SHINJI_BAYCREST_FENNEKIN, BaycrestCity_Text_ShinjiDefeated)
            break
        case 2:
            trainerbattle_no_intro(TRAINER_SHINJI_BAYCREST_QUAXLY, BaycrestCity_Text_ShinjiDefeated)
            break
    }

    msgbox(BaycrestCity_Text_Rival2M)
    giveitem(ITEM_TM_SWIFT)
    msgbox(BaycrestCity_Text_Rival3M)
    closemessage

    applymovement(OBJ_RIVAL, RivalLeave)
    waitmovement(0)
    hideobjectat(OBJ_RIVAL, MAP_BAYCREST_CITY)

    setvar(VAR_BAYCREST_CITY_STATE, 1)
    setflag(FLAG_HIDE_BAYCREST_CITY_RIVAL)

    releaseall
}


movement ApproachRival1 {
    walk_up * 2
    walk_right
    walk_in_place_faster_up
}

movement ApproachRival2 {
    walk_up * 2
}

movement LookLeft {
    emote_exclamation_mark
    walk_in_place_faster_left
}

movement LookDown {
    emote_exclamation_mark
    walk_in_place_faster_down
}

movement RivalLeave {
    walk_left 
    walk_up * 6
}

text BaycrestCity_Text_Rival1F {
    format("{RIVAL}: Hey, {PLAYER}{KUN}! I've been waiting for you!\p"
            "I've already registered for the Toshou League, so I need to practice battling.\p"
            "You're the perfect person to help me with that!\p"
            "Let's battle!"
    )
}

text BaycrestCity_Text_Rival2F { //After battle, GIVE TM
    format("{RIVAL}: Wow, you're really good at this!\p"
            "I guess I'll have to train harder if I want to beat you!\p"
            "Here, take this TM as a thank you for the battle!\p"
    )
}

text BaycrestCity_Text_Rival3F {
    format("{RIVAL}: It's the TM for Swift! It's a great move that never misses!\p"
            "I hope you find it useful!\p"
            "I'll see you later!"
    )
}

text BaycrestCity_Text_KanamiDefeated {
    format( "My battle career started off with a loss...\p"
            "Geez, this is the worst!"
    )
}

text BaycrestCity_Text_ShinjiDefeated {
    format( "My first battle ended in a loss...\p"
            "I can't believe it!"
    )
}

text BaycrestCity_Text_Rival1M {
    format("{RIVAL}: Oh, hey, {PLAYER}{KUN}!\nThere you are!\p"
            "I've been waiting for you!\p"
            "Just so you know, I've already registered for the Toshou League.\p"
            "I need to practice, so you and I are going to have a battle!\p"
            "Let's do this!"
    )
}

text BaycrestCity_Text_Rival2M {
    format("{RIVAL}: You're pretty good at this, {PLAYER}{KUN}!\p"
            "I'm gonna train hard so I can beat you next time!\p"
            "I'll give you this since you helped me practice!\p"
    )
}

text BaycrestCity_Text_Rival3M {
    format("{RIVAL}: This TM is for the move Swift!\p"
            "It never misses, so it's a great move to have!\p"
            "I'll see you later, {PLAYER}{KUN}!"
    )
}

script BaycrestCity_EventScript_TrendyTrainer {
    lock
    faceplayer
    msgbox(Baycrest_Text_TrendyTrainer)
    release
}

text Baycrest_Text_TrendyTrainer {
    format( "These days double battles are all the rage!\p"
            "It's when you and your opponent each use two Pokemon in battle... AT THE SAME TIME!\p"
            "I heard that Hoennians brought the idea over to Toshou!\p"
            "I wonder if it'll catch on in places like Kanto and Johto too!"
    )
}

script BaycrestCity_EventScript_NostalgicGrandpa {
    lock
    faceplayer
    msgbox(Baycrest_Text_NostalgicGrandpa)
    release
}

text Baycrest_Text_NostalgicGrandpa {
    format( "Back in my day, we didn't have fancy things like EXP Shares!\p"
            "We had to rely on our own strength to train our Pokemon!\p"
            "It was tough, but it made us stronger!\p"
            "I wonder if the kids these days could handle it..."
    )
}

script BaycrestCity_EventScript_UnsureYoungster {
    lock
    faceplayer
    msgbox(Baycrest_Text_UnsureYoungster)
    release
}

text Baycrest_Text_UnsureYoungster {
    format( "I want to challenge the Toshou League, but I'm not sure if I'm ready...\p"
            "I asked my friend for advice, but she told me that my Lillipup is too friendly to fight seriously...\p"
            "I don't know what to do... I don't want to give up on my dream, but I don't want to hurt my Pokemon either..."
    )
}

script BaycrestCity_EventScript_HonestLass {
    lock
    faceplayer
    msgbox(BaycrestCity_Text_HonestLass)
    release
}

text BaycrestCity_Text_HonestLass {
    format( "My childhood friend wants to challenge the Toshou League with a Lillipup!\p"
            "I told him that I think he's got a long way to go before he's ready, but he seemed really discouraged...\p"
            "I don't want to see him give up on his dream, but I don't want him to give him false hope either."
    )
}

script BaycrestCity_EventScript_Lillipup {
    lock
    faceplayer
    playmoncry(SPECIES_LILLIPUP, CRY_MODE_NORMAL)
    msgbox(format("Lillipup: Terieee!"))
    waitmoncry
    release
}

script BaycrestCity_EventScript_HelpfulGentleman { //Recommends you check out Route 502 for training before challenging the gym
    lock
    faceplayer
    msgbox(BaycrestCity_Text_HelpfulGentleman)
    release
}

text BaycrestCity_Text_HelpfulGentleman {
    format( "If you're planning to challenge the Baycrest City Gym, you should train on Route 502 first!\p"
            "There are a lot of Pokémon there that can help you prepare for the Gym Leader's team!"
    )
}



script BaycrestCity_Sign_City {
    lockall
    msgbox(format("Baycrest City\n“A city of the sea breeze and the setting sun.”"))
    releaseall
}

script BaycrestCity_Sign_RockSmashManHouse {
    lockall
    msgbox(format("Rock Smash Man's House"))
    releaseall
}

script BaycrestCity_Sign_Gym {
    lockall
    msgbox(BaycrestCity_Text_GymSign)
    releaseall
}

text BaycrestCity_Text_GymSign {
    format( "Baycrest City Pokémon Gym\nGym Leader: Riku\p"
            "“The Grounded Wunderkind”"

    )
}