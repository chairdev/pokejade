//VAR_STARRIDGE_CITY_STATE
// 0: Melody has not been defeated
// 1: Scientist has informed about the hostage situation
// 2: Need to rescue the boss
// 3: Defeated the grunts guarding Starridge Ruins B1F
// 4: Defeated the grunts guarding Starridge Ruins B2F

const OBJ_RIVAL = 1
const OBJ_SCIENTIST = 2

mapscripts StarridgeCity_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
    }
    MAP_SCRIPT_ON_LOAD {
         switch(var(VAR_STARRIDGE_CITY_STATE)) {
            case 2:
                setobjectxy(OBJ_SCIENTIST, 32, 5)
                break
        }
    }
}

script StarridgeCity_EventScript_Rival {
    lockall
    faceplayer

    speakername("{RIVAL}")

    checkplayergender
    if(var(VAR_RESULT) == MALE)
    {
        playbgm(MUS_ENCOUNTER_MAY, 0)
        msgbox(StarridgeCity_Text_Rival1F, MSGBOX_YESNO)

        if(var(VAR_RESULT) == YES)
        {
            msgbox(StarridgeCity_Text_RivalF_Accept)
           switch(var(VAR_STARTER_MON)) {
            case 0:
                trainerbattle_no_intro(TRAINER_HOLLY_DOLOMITE_CHESPIN, StarridgeCity_Text_HollyDefeated)
                break
            case 1:
                trainerbattle_no_intro(TRAINER_HOLLY_DOLOMITE_FENNEKIN, StarridgeCity_Text_HollyDefeated)
                break
            case 2:
                trainerbattle_no_intro(TRAINER_HOLLY_DOLOMITE_FROAKIE, StarridgeCity_Text_HollyDefeated)
                break
            }

            speakername("{RIVAL}")
            msgbox(StarridgeCity_Text_Rival2F)

            fadescreen(FADE_TO_BLACK)
            removeobject(OBJ_RIVAL)
            setflag(FLAG_HIDE_STARRIDGE_CITY_RIVAL)
            fadescreen(FADE_FROM_BLACK)
            
        }
        else
        {
            msgbox(StarridgeCity_Text_RivalF_Refuse)
        }
    }
    else
    {
        playbgm(MUS_ENCOUNTER_BRENDAN, 0)
        msgbox(StarridgeCity_Text_Rival1M, MSGBOX_YESNO)

        if(var(VAR_RESULT) == YES)
        {
            msgbox(StarridgeCity_Text_RivalM_Accept)
           switch(var(VAR_STARTER_MON)) {
            case 0:
                trainerbattle_no_intro(TRAINER_CADEN_DOLOMITE_CHESPIN, StarridgeCity_Text_CadenDefeated)
                break
            case 1:
                trainerbattle_no_intro(TRAINER_CADEN_DOLOMITE_FENNEKIN, StarridgeCity_Text_CadenDefeated)
                break
            case 2:
                trainerbattle_no_intro(TRAINER_CADEN_DOLOMITE_FROAKIE, StarridgeCity_Text_CadenDefeated)
                break
            }

            speakername("{RIVAL}")
            msgbox(StarridgeCity_Text_Rival2F)

            fadescreen(FADE_TO_BLACK)
            removeobject(OBJ_RIVAL)
            setflag(FLAG_HIDE_STARRIDGE_CITY_RIVAL)
            fadescreen(FADE_FROM_BLACK)
        }
    }


    fadedefaultbgm
    releaseall
}

text StarridgeCity_Text_Rival1F {
    format("Hey, {PLAYER}!\p"
            "Have you been fighting lots of Trainers?\p"
            "I'm sure you have! I think that's why you and your Pokémon are so strong-looking!\p"
            "I wish I could say the same about myself, though...\p"
            "I'm still not sure if I'm training my Pokémon the right way.\p"
            "But I'm getting better every day!\p"
            "Hey, I know!\nLet's have a battle!"
    )
}

text StarridgeCity_Text_RivalF_Accept {
    format("{PLAYER}! I'm gonna give you a run for your money!")
}

text StarridgeCity_Text_RivalF_Refuse {
    format("Oh, okay... I guess I'll see you later, then!")
}

text StarridgeCity_Text_HollyDefeated {
   format("Urgh!\nI'm never gonna get better at battling!")
}

text StarridgeCity_Text_Rival2F {
    format("Man, I even Terastalized and everything...\p"
            "I've got to figure out what went wrong!\p"
            "Thanks for the battle, though, {PLAYER}!\nYou're an inspiration.\p"
            "See you around!"
    )
}

text StarridgeCity_Text_Rival1M {
    format("Hey, {PLAYER}! \p"
           "How's it going with all the battling? Hope you're keeping up!\p"
           "I've been pushing hard too--think I've got some new moves that might surprise you.\p"
           "Want to see how you stack up against me now? Let's have a quick battle!\p"
           "Bet it'll be close!"
    )
}

text StarridgeCity_Text_RivalM_Accept {
    format("Alright, {PLAYER}! Let's see which of us really has the edge!")
}

text StarridgeCity_Text_RivalM_Refuse {
    format("Really? Not feeling up to it? Guess I'll have to save these moves for next time.")
}

text StarridgeCity_Text_CadenDefeated {
   format("Whoa! You pulled that one off... nice job, {PLAYER}!")
}

text StarridgeCity_Text_Rival2M {
    format("Even with Terastalizing, huh… Guess you really came through!\p"
           "But hey, this isn't the end of it. Next time, it'll be a different story!\p"
           "Just you wait!")
}

script StarridgeCity_Trigger_HostageSituation1 {
    clearflag(FLAG_HIDE_STARRIDGE_RUINS_B1F_UPSILON_GRUNTS)
    addobject(OBJ_SCIENTIST)
    setobjectxy(OBJ_SCIENTIST, 29, 9)
    call(StarridgeCity_EventScript_HostageSituation)
    applymovement(OBJ_SCIENTIST, StarridgeCity_Movement_ScientistWait1)
    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerWalkDown)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    release
}

script StarridgeCity_Trigger_HostageSituation2 {
    clearflag(FLAG_HIDE_STARRIDGE_RUINS_B1F_UPSILON_GRUNTS)
    addobject(OBJ_SCIENTIST)
    setobjectxy(OBJ_SCIENTIST, 30, 9)
    call(StarridgeCity_EventScript_HostageSituation)
    applymovement(OBJ_SCIENTIST, StarridgeCity_Movement_ScientistWait2)
    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerWalkDown)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    release
}

script StarridgeCity_Trigger_HostageSituation3 {
    clearflag(FLAG_HIDE_STARRIDGE_RUINS_B1F_UPSILON_GRUNTS)
    addobject(OBJ_SCIENTIST)
    setobjectxy(OBJ_SCIENTIST, 31, 9)
    call(StarridgeCity_EventScript_HostageSituation)
    applymovement(OBJ_SCIENTIST, StarridgeCity_Movement_ScientistWait3)
    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerWalkDown)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    release
}

script StarridgeCity_Trigger_GoSaveBoss1 {
    lockall

    applymovement(OBJ_SCIENTIST, StarridgeCity_Movement_ScientistApproachPlayer1)
    waitmovement(OBJ_SCIENTIST)
    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerFaceRight)
    waitmovement(OBJ_EVENT_ID_PLAYER)

    speakername("Scientist")
    msgbox(StarridgeCity_Text_HostageSituation_Yes)
    closemessage

    applymovement(OBJ_SCIENTIST, StarridgeCity_Movement_ScientistWalkBack1)
    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerWalkDown2)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    release
}

script StarridgeCity_Trigger_GoSaveBoss2 {
    lockall

    applymovement(OBJ_SCIENTIST, StarridgeCity_Movement_ScientistApproachPlayer2)
    waitmovement(OBJ_SCIENTIST)
    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerFaceRight)
    waitmovement(OBJ_EVENT_ID_PLAYER)

    speakername("Scientist")
    msgbox(StarridgeCity_Text_HostageSituation_Yes)
    closemessage

    applymovement(OBJ_SCIENTIST, StarridgeCity_Movement_ScientistWalkBack2)
    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerWalkDown2)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    release
}

script StarridgeCity_Trigger_GoSaveBoss3 {
    lockall

    applymovement(OBJ_SCIENTIST, StarridgeCity_Movement_ScientistFaceLeft)
    waitmovement(OBJ_SCIENTIST)
    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerFaceRight)
    waitmovement(OBJ_EVENT_ID_PLAYER)

    speakername("Scientist")
    msgbox(StarridgeCity_Text_HostageSituation_Yes)
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerWalkDown2)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    release
}

script StarridgeCity_EventScript_HostageSituation {
    lockall
    clearflag(FLAG_HIDE_DOLOMITE_GROTTO_GRUNT_AND_GURU)

    applymovement(OBJ_SCIENTIST, StarridgeCity_Movement_ScientistApproachPlayer)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, StarridgeCity_Movement_PlayerFaceDown)

    speakername("Scientist")
    msgbox(StarridgeCity_Text_HostageSituation_Scientist1, MSGBOX_YESNO)
    while (var(VAR_RESULT) != YES)
    {
        msgbox(StarridgeCity_Text_HostageSituation_ButWhy, MSGBOX_YESNO)
    }
    msgbox(StarridgeCity_Text_HostageSituation_Scientist2)
    setvar(VAR_STARRIDGE_CITY_STATE, 2)
    closemessage
}

movement StarridgeCity_Movement_PlayerFaceDown {
    walk_in_place_down
}

movement StarridgeCity_Movement_PlayerFaceRight {
    walk_in_place_right
}

movement StarridgeCity_Movement_PlayerWalkDown {
    walk_down * 3
}

movement StarridgeCity_Movement_PlayerWalkDown2 {
    walk_down
}

movement StarridgeCity_Movement_ScientistApproachPlayer {
    walk_fast_up * 5
}

movement StarridgeCity_Movement_ScientistApproachPlayer1 {
    walk_fast_left * 2
}

movement StarridgeCity_Movement_ScientistApproachPlayer2 {
    walk_fast_left
}

movement StarridgeCity_Movement_ScientistWalkBack1 {
    walk_fast_right * 2
}

movement StarridgeCity_Movement_ScientistWalkBack2 {
    walk_fast_right
}

movement StarridgeCity_Movement_ScientistFaceLeft {
    walk_in_place_fast_left
}

movement StarridgeCity_Movement_ScientistWait1 {
    walk_down
    walk_right * 3
}

movement StarridgeCity_Movement_ScientistWait2 {
    walk_down
    walk_right * 2
}

movement StarridgeCity_Movement_ScientistWait3 {
    walk_down
    walk_right
}

text StarridgeCity_Text_HostageSituation_Scientist1 {
    format("You there! Please, I need your help!\p"
            "T-Team Upsilon... they... they barged into the ruins on Route 306 and are holding our boss hostage...\p"
            "It happened so fast! I was about to head out when a couple of those Team Upsilon grunts stormed in.\p"
            "They grabbed our boss and ran off! I tried to stop them, but they were too fast!\p"
            "I've been running around trying to find someone to help, but no one's been able to do anything!\p"
            "You look strong--can you go after them?"
    )
}

text StarridgeCity_Text_HostageSituation_Scientist2 {
    format("Thank you! Let me tell you what I know...\p"
            "They said something about... the ruins on Route 306... I think that's where they're headed.\p"
            "They're using our boss for something... something dangerous, I just know it."
    )
}

text StarridgeCity_Text_HostageSituation_ButWhy {
    format("I understand... but... please! I'm begging you! You're the only one here that's strong enough to help!")
}

text StarridgeCity_Text_HostageSituation_Yes {
    format("They took our boss to the Starridge Ruins on Route 306!\p"
            "I'll be waiting here in case they come back. Please, hurry!")
}

script StarridgeCity_EventScript_Scientist {
    lock
    faceplayer

    speakername("Scientist")
    msgbox(StarridgeCity_Text_HostageSituation_Yes)

    release
}

script StarridgeCity_EventScript_ShadyMan { //gives player a dusk ball
    lock
    faceplayer

    if(!flag(FLAG_RECEIVED_DUSK_BALL))
    {
        msgbox(format("...Hey, kid.\nYou look like you could use this.\p"
                      "It's a Dusk Ball. It's good for catching Pokémon in dark places.\p"
                      "You never know when you might need it.\p"
                      "Take it. It's on the house."))

        giveitem(ITEM_DUSK_BALL, 1)
        setflag(FLAG_RECEIVED_DUSK_BALL)
    }
    else
    {
        msgbox(format("You already got a free Dusk Ball from me.\p"
                      "Don't be greedy."))
    }
    release
}