const OBJ_RIVAL = 2

//Hawthorne Lab State
//0: Go to the Research Lab
//1: Find Rival
//2: Saved Rival
//3: Done?

mapscripts EverwoodTown_HawthornResearchLab_MapScripts {
    MAP_SCRIPT_ON_TRANSITION : EverwoodTown_HawthornResearchLab_MapScript_On_Transition
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_HAWTHORNE_LAB_STATE, 2: EverwoodTown_HawthornResearchLab_MapScript_ReceiveStarter
    ]
}

script EverwoodTown_HawthornResearchLab_MapScript_On_Transition {
    call(Common_EventScript_SetupRivalGfxId)
}

script EverwoodTown_HawthornResearchLab_MapScript_ReceiveStarter {
    lockall 

    applymovement(OBJ_EVENT_ID_PLAYER, ReceiveStarter_FaceUp)

    bufferleadmonspeciesname(STR_VAR_1)
    msgbox(ReceiveStarter_HawthorneText1)
    closemessage
    msgbox(format("You received the {STR_VAR_1}!"))
    waitmessage
	playfanfare(MUS_OBTAIN_ITEM)
	waitfanfare
    speakername("Hawthorne")
    msgbox(format("By the way, would you like to give it a nickname?"), MSGBOX_YESNO)
    closemessage    

    if(var(VAR_RESULT) == 1)
    {
        setvar(VAR_0x8004, 0)
	    call(Common_EventScript_NameReceivedPartyMon)
    }
    else
    {
        speakername("Hawthorne")
        msgbox(format("That's fine. I hope you'll take good care of it."))
    }

    closemessage
    giveitem(ITEM_POKE_BALL, 5)
    msgbox(ReceiveStarter_HawthorneText2)

    msgbox(ReceiveStarter_HawthorneText3)
    closemessage
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox(format("You received the Pokédex!"))
    waitfanfare
    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText4)
    waitmessage
    speakername("{RIVAL}")
    msgbox(ReceiveStarter_RivalText1)
    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText5)
    speakername("{RIVAL}")
    msgbox(ReceiveStarter_RivalText2)
    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText6)
    closemessage
    applymovement(OBJ_RIVAL, ReceiveStarter_FaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, ReceiveStarter_FaceLeft)
    speakername("{RIVAL}")
    msgbox(ReceiveStarter_RivalText3)
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, ReceiveStarter_FaceDown)
    applymovement(OBJ_RIVAL, ReceiveStarter_RivalLeave)
    waitmovement(0)
    hideobjectat(OBJ_RIVAL, MAP_EVERWOOD_TOWN_HAWTHORN_RESEARCH_LAB)

    applymovement(OBJ_EVENT_ID_PLAYER, ReceiveStarter_FaceUp)
    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText7)
    closemessage
    giveitem(ITEM_EXP_SHARE, 1)

    setflag(FLAG_EXP_SHARE)
    setflag(FLAG_HIDE_EVERWOOD_TOWN_RESEARCH_LAB_RIVAL)
    setflag(FLAG_SYS_POKEDEX_GET)
	special(SetUnlockedPokedexFlags)
    clearflag(FLAG_HIDE_EVERWOOD_TOWN_MOM_OUTSIDE)
    setvar (VAR_CABLE_CLUB_TUTORIAL_STATE, 1)
    setvar(VAR_EVERWOOD_TOWN_STATE, 3)
    setvar(VAR_HAWTHORNE_LAB_STATE, 3)
    

    releaseall
}

text ReceiveStarter_HawthorneText1 { //After they fought off the wild Pokémon
    format( "...\p"
            "...I see. So that's what happened.\p"
            "I'm glad you're both safe.\p"
            "By the way, {PLAYER}{KUN}, may I see the Pokémon you chose?\p"
            "I'd like to see how it's doing.\p"
            "Ah, I see. You chose {STR_VAR_1}.\nIt seems to have taken a liking to you.\p"
            "I hope you'll take good care of it."
            )
}

text ReceiveStarter_HawthorneText2 {
    "These are Poké Balls.\nThey're used to catch wild Pokémon.\p"
    "Please, take these with you."
}

text ReceiveStarter_HawthorneText3 {
    format( "Let's see...what else?\p"
            "Ah, yes! This!"
            )
}

text ReceiveStarter_HawthorneText4 {
    format( "This is a Pokédex.\nIt's a high-tech encyclopedia that records data on every Pokémon you encounter.\p"
            "As you travel through Toshou, I'd like you to help me with my research by filling it with information on the various Pokémon species that inhabit this region.\p"
            
            "You two have already proven that you're ready to take on the responsibilities of a Pokémon Trainer.\p"
            "I'm sure that if you try your best, you'll both become great Trainers.\p"
            "I wish you both the best of luck on your journey!"
            )
}

text ReceiveStarter_RivalText1 {
    format("(He says that, but I didn't really intend to become a Trainer...)"
           )
}

text ReceiveStarter_HawthorneText5 {
    format("Hm? Did you say something, {RIVAL}?"
           )
}

text ReceiveStarter_RivalText2 {
    format("No, nothing!\nLet's go do our best, {PLAYER}{KUN}!\p"
            "Ahaha...ha..."
           )
}

text ReceiveStarter_HawthorneText6 {
    format("You two must first register for the Toshou League at the Pokémon Center.\p"
            "The nearest one is in Baycrest City, past Route 501."
           )
}

text ReceiveStarter_RivalText3 {
    format("I'll be waiting for you at the Pokémon Center, {PLAYER}{KUN}!\p"
            "See you!"
           )
}

text ReceiveStarter_HawthorneText7 {
    format("I almost forgot! I have something else for you.\p"
           "This is an Exp. Share.\nIt's a special item that allows Pokémon to gain experience points even if they don't participate in battle.\p"
           "I hope you'll find it useful on your journey."
           )
}

movement ReceiveStarter_FaceUp {
    walk_in_place_faster_up
}

movement ReceiveStarter_FaceDown {
    walk_in_place_faster_down
}

movement ReceiveStarter_FaceLeft {
    walk_in_place_faster_left
}

movement ReceiveStarter_FaceRight {
    walk_in_place_faster_right
}

movement ReceiveStarter_RivalLeave {
    walk_down * 5
}





script EverwoodTown_HawthornResearchLab_EventScript_Hawthorne {
    lockall
    faceplayer
    speakername("Hawthorne")
    switch(var(VAR_HAWTHORNE_LAB_STATE))
    {
        case 0:
            msgbox(MeetHawthorneText1)

            checkplayergender()
            if(var(VAR_RESULT) == MALE)
            {
                msgbox(MeetHawthorneText2A)
            }
            else
            {
                msgbox(MeetHawthorneText2B)
            }

            setvar(VAR_HAWTHORNE_LAB_STATE, 1)
            setvar(VAR_EVERWOOD_TOWN_STATE, 2)
        case 1:
            msgbox(GoLookForRivalText1)
    }
    
    closemessage
    releaseall
}

text MeetHawthorneText1 {
    format("Ah, there you are!\nYou must be {PLAYER}{KUN}.\l"
           "I've been waiting for you!\p"
           "Where's {RIVAL}?\p"
           "... What do you mean you don't know who that is?\p"
           "But {RIVAL} was supposed to bring you the bag with the Poké Balls!"
           )
}

text MeetHawthorneText2A {
    format("She said she couldn't sit still and wait for you to arrive and took off.\p"
           "{PLAYER}{KUN}, I'm sorry to ask this of you, but could you go find her for me?\p"
            "I'm sure she's just around the corner."
           )
}

text MeetHawthorneText2B {
    format("He said he couldn't sit still and wait for you to arrive and took off.\p"
           "{PLAYER}{KUN}, I'm sorry to ask this of you, but could you go find him for me?\p"
            "I'm sure he's just around the corner."
           )          
}

text GoLookForRivalText1 {
   format("Please, {PLAYER}{KUN}, go look for {RIVAL}!\p"
           "The bag with the Poké Balls is very important!"
           )
}