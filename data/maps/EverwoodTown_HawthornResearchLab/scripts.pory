const OBJ_RIVAL = 2

//Hawthorne Lab State
//0: Go to the Research Lab
//1: Find Rival
//2: Saved Rival
//3: Done?

mapscripts EverwoodTown_HawthornResearchLab_MapScripts {
    MAP_SCRIPT_ON_TRANSITION : EverwoodTown_HawthornResearchLab_MapScript_On_Transition
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_HAWTHORNE_LAB_STATE, 2: EverwoodTown_HawthornResearchLab_MapScript_ReceiveStarter
    ]
}

script EverwoodTown_HawthornResearchLab_MapScript_On_Transition {
    call(Common_EventScript_SetupRivalGfxId)
}

script EverwoodTown_HawthornResearchLab_MapScript_ReceiveStarter {
    lockall 

    applymovement(OBJ_EVENT_ID_PLAYER, ReceiveStarter_FaceUp)

    bufferleadmonspeciesname(STR_VAR_1)
    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText1)
    closemessage
    playfanfare(MUS_EVOLVED)
    msgbox(format("You've received the {STR_VAR_1}!"))
    waitfanfare
    msgbox("Would you like to give it a nickname?", MSGBOX_YESNO)
    closemessage    

    if (var(VAR_RESULT) == 1) {
        setvar(VAR_0x8004, 0)
        call(Common_EventScript_NameReceivedPartyMon)
    } else {
        speakername("Hawthorne")
        msgbox(format("That's alright. Please take care of it well."))
    }

    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText2)
    closemessage    
    closemessage
    giveitem(ITEM_POKE_BALL, 5)
    msgbox(ReceiveStarter_HawthorneText3)

    playfanfare(MUS_OBTAIN_ITEM)
    msgbox("You've received the Pokédex!")
    waitfanfare

    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText4)
    waitmessage

    // Now continue with Rival dialogue
    speakername("{RIVAL}")
    msgbox(ReceiveStarter_RivalText1)

    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText5)

    speakername("{RIVAL}")
    msgbox(ReceiveStarter_RivalText2)

    // Giving Tera Orb here, before the Rival leaves
    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText7) // Tera Orb Dialogue
    closemessage
    giveitem(ITEM_TERA_ORB, 1)
    waitfanfare

    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText8) // Tera Orb explanation
    closemessage

    // Rival comments on the Tera Orb
    speakername("{RIVAL}")
    msgbox(ReceiveStarter_RivalText_TeraOrb)
    closemessage

    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText6)
    
    closemessage
    applymovement(OBJ_RIVAL, ReceiveStarter_FaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, ReceiveStarter_FaceLeft)

    speakername("{RIVAL}")
    msgbox(ReceiveStarter_RivalText3)
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, ReceiveStarter_FaceDown)
    applymovement(OBJ_RIVAL, ReceiveStarter_RivalLeave)
    waitmovement(0)
    hideobjectat(OBJ_RIVAL, MAP_EVERWOOD_TOWN_HAWTHORN_RESEARCH_LAB)

    applymovement(OBJ_EVENT_ID_PLAYER, ReceiveStarter_FaceUp)

    speakername("Hawthorne")
    msgbox(ReceiveStarter_HawthorneText9) // Exp. Share Dialogue
    closemessage
    giveitem(ITEM_EXP_SHARE, 1)
    waitfanfare

    setflag(FLAG_EXP_SHARE)
    setflag(B_FLAG_TERA_ORB_CHARGED)
    setflag(FLAG_SYS_POKEDEX_GET)
    special(SetUnlockedPokedexFlags)

    clearflag(FLAG_HIDE_EVERWOOD_TOWN_MOM_OUTSIDE)
    setvar(VAR_CABLE_CLUB_TUTORIAL_STATE, 1)
    setvar(VAR_EVERWOOD_TOWN_STATE, 3)
    setvar(VAR_HAWTHORNE_LAB_STATE, 3)

    releaseall
}


// New Text for Rival's Comment on the Tera Orb
text ReceiveStarter_RivalText_TeraOrb {
    format("Wow, a Tera Orb, huh?\p"
           "I heard those things let your Pokémon change its type or something.\p"
           "If the original type is the same as its Tera type, moves of that type get an even bigger boost in power.\p"
           "That's what I heard, anyway."
              )
}

text ReceiveStarter_HawthorneText1 {
    format("...I see. So, that's what happened.\p"
           "I'm relieved that both you and {RIVAL} are safe.\p"
           "By the way, {PLAYER}, may I take a look at the Pokémon you've chosen?\p"
           "It seems {STR_VAR_1} has already taken a liking to you!\p"
           "Take good care of it, alright?"
           )
}

text ReceiveStarter_HawthorneText2 {
    format("These are Poké Balls, used for catching wild Pokémon.\p"
    "Here, take these as well. They'll be useful on your journey.")
}

text ReceiveStarter_HawthorneText3 {
    format("Now, let's see...\p"
           "Ah! I have something else for you."
           )
}

text ReceiveStarter_HawthorneText4 {
    format("This is a Pokédex.\p"
           "It's a high-tech encyclopedia that records information on every Pokémon you encounter.\p"
           "As you travel through Toshou, I'd like you to help me with my research by filling it with details on the various Pokémon species you'll meet.\p"
           "I believe you're both more than capable of doing so."
           )
}

text ReceiveStarter_RivalText1 {
    format("(He says that, but I didn't really plan on becoming a Trainer...)"
           )
}

text ReceiveStarter_HawthorneText5 {
    format("Hm? Did you say something, {RIVAL}?")
}

text ReceiveStarter_RivalText2 {
    format("No, nothing! Let's just do our best, {PLAYER}!\p"
    "Ahaha...")
}

text ReceiveStarter_HawthorneText6 {
    format( "That's right! It lasts until the end of battle or until your Pokémon faints.\p"
            "Oh, and one more thing!\p"
            "Before you two set off, don't forget to register for the Toshou League at the nearest Pokémon Center.\p"
            "The closest one is in Baycrest City, just past Route 301."
           )
}

text ReceiveStarter_RivalText3 {
    format("I'll be waiting for you at the Pokémon Center, {PLAYER}!\p"
           "See you there!"
           )
}

text ReceiveStarter_HawthorneText7 {
    format("One last thing!\p"
           "This is a Tera Orb.\p"
           "It allows your Pokémon to Terastalize, granting them enhanced power and an advantage in battle."
           )
}

text ReceiveStarter_HawthorneText8 {
    format("Though Terastal energy was originally discovered in Paldea, recent research has revealed that it exists in other regions as well, like Kitakami.\p"
           "Here in Toshou, the energy flows through certain areas, much like the ley lines of nature.\p"
           "With this Tera Orb, your Pokémon can harness this energy by pressing the START Button while choosing a move in battle.\p"
           "Use it wisely, as it can only be used once before needing to be recharged at a Pokémon Center."
           )
}

text ReceiveStarter_HawthorneText9 {
    format("And lastly, here's an Exp. Share.\p"
    "It will help your team grow stronger together, even if they don't fight.")
}

movement ReceiveStarter_FaceUp {
    walk_in_place_faster_up
}

movement ReceiveStarter_FaceDown {
    walk_in_place_faster_down
}

movement ReceiveStarter_FaceLeft {
    walk_in_place_faster_left
}

movement ReceiveStarter_FaceRight {
    walk_in_place_faster_right
}

movement ReceiveStarter_RivalLeave {
    walk_down * 5
}





script EverwoodTown_HawthornResearchLab_EventScript_Hawthorne {
    lockall
    faceplayer
    speakername("Hawthorne")
    switch(var(VAR_HAWTHORNE_LAB_STATE))
    {
        case 0:
            msgbox(MeetHawthorneText1)

            checkplayergender()
            if(var(VAR_RESULT) == MALE)
            {
                msgbox(MeetHawthorneText2A)
            }
            else
            {
                msgbox(MeetHawthorneText2B)
            }

            setvar(VAR_HAWTHORNE_LAB_STATE, 1)
            setvar(VAR_EVERWOOD_TOWN_STATE, 2)
        case 1:
            msgbox(GoLookForRivalText1)
    }
    
    closemessage
    releaseall
}

text MeetHawthorneText1 {
    format("Ah, there you are!\nYou must be {PLAYER}.\l"
           "I've been waiting for you!\p"
           "Where's {RIVAL}?\p"
           "... What do you mean you don't know who that is?\p"
           "But {RIVAL} was supposed to bring you the bag with the Poké Balls!"
           )
}

text MeetHawthorneText2A {
    format("She said she couldn't sit still and wait for you to arrive and took off.\p"
           "{PLAYER}, I'm sorry to ask this of you, but could you go find her for me?\p"
            "I'm sure she's just around the corner."
           )
}

text MeetHawthorneText2B {
    format("He said he couldn't sit still and wait for you to arrive and took off.\p"
           "{PLAYER}, I'm sorry to ask this of you, but could you go find him for me?\p"
            "I'm sure he's just around the corner."
           )          
}

text GoLookForRivalText1 {
   format("Please, {PLAYER}, go look for {RIVAL}!\p"
           "The bag with the Poké Balls is very important!"
           )
}