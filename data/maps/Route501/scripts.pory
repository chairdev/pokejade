const OBJ_RIVAL = 1
const OBJ_PIDOVE1 = 3
const OBJ_PIDOVE2 = 4

mapscripts Route501_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
    }
}

script Route501_Trigger_WalkToRivalLeft
{
    lockall

    applymovement(OBJ_EVENT_ID_PLAYER, PlayerMoveToRivalLeft)
    waitmovement(0)

    checkplayergender
    goto_if_eq(VAR_RESULT, MALE, Route501_RivalMaya)
}

script Route501_Trigger_WalkToRivalRight
{
    lockall

    applymovement(OBJ_EVENT_ID_PLAYER, PlayerMoveToRivalRight)
    waitmovement(0)

    checkplayergender
    goto_if_eq(VAR_RESULT, MALE, Route501_RivalMaya)
}

movement PlayerMoveToRivalLeft {
    walk_right
    walk_up * 2
    face_right
}

movement PlayerMoveToRivalRight {
    walk_up * 2
    face_right
}

script Route501_RivalMaya {
    applymovement(OBJ_RIVAL, RivalNoticePlayer)
    waitmovement(0)

    playbgm(MUS_ENCOUNTER_MAY, 0)

    msgbox(RivalText1F)
    
    applymovement(OBJ_RIVAL, RivalLookAround)
    waitmovement(0)

    msgbox(format("Oh, there it is!"))
    
    applymovement(OBJ_RIVAL, RivalApproachBag)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerApproachBag)
    waitmovement(1)

    msgbox(RivalText2F)

    playbgm(MUS_HELP, 0)

    setobjectxy(OBJ_PIDOVE1, 1, 17)
    setobjectxy(OBJ_PIDOVE2, 1, 17)
    applymovement(OBJ_PIDOVE1, PidoveApproachPlayer)
    applymovement(OBJ_PIDOVE2, PidoveApprachRival)
    waitmovement(1)

    playmoncry(SPECIES_PIDOVE, CRY_MODE_NORMAL)

    applymovement(OBJ_EVENT_ID_PLAYER, ExclemationMark)
    applymovement(OBJ_RIVAL, ExclemationMark)
    waitmovement(1)
    applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
    applymovement(OBJ_RIVAL, FaceUp)

    msgbox(RivalText3F)

    applymovement(OBJ_EVENT_ID_PLAYER, FaceRight)
    applymovement(OBJ_RIVAL, FaceDown)

    setflag(FLAG_SYS_POKEMON_GET)
    fadescreen(FADE_TO_BLACK)
    removeobject(OBJ_PIDOVE1)
    removeobject(OBJ_PIDOVE2)
	special(ChooseStarter)
	waitstate

    bufferleadmonspeciesname(STR_VAR_1)
    msgbox(RivalText4F)

    special(HealPlayerParty)
    setflag(FLAG_ROUTE_501_SAVED_RIVAL)
    clearflag(FLAG_HIDE_EVERWOOD_TOWN_RESEARCH_LAB_RIVAL)
    setvar(VAR_ROUTE501_STATE, 1)
    setvar(VAR_SANZASHI_LAB_STATE, 2)

    warp(MAP_EVERWOOD_TOWN_HAWTHORN_RESEARCH_LAB, 6, 5)
}

movement RivalNoticePlayer {
    emote_exclamation_mark
    face_left
}

movement RivalLookAround {
    walk_in_place_up
    walk_in_place_down
    walk_in_place_right
    face_right
    emote_exclamation_mark
}

movement RivalApproachBag {
    walk_up
    walk_right * 4
    face_up
}

movement PlayerApproachBag {
    walk_right * 4
    face_right
}

movement PidoveApproachPlayer {
    walk_faster_right * 10
}

movement PidoveApprachRival {
    walk_faster_down* 6
}

movement ExclemationMark {
    emote_exclamation_mark
}

movement FaceUp {
    face_up
}

movement FaceDown {
    face_down
}

movement FaceLeft {
    face_left
}

movement FaceRight {
    face_right
}

text RivalText1F { //female rival
    format("Huh? What the...?\p"
           "Oh! Are you the {PLAYER}{KUN} that just moved in next door?\p"
           "I'm your neighbor, {RIVAL}! Nice to meet you!\p"
            "Oh, right! I have something for you from Professor Sanzashi!\p"
            "I went to your house to deliver it, but your mom said you were asleep.\p"
            "I didn't want to wake you up, so I decided to wander around a bit.\p"
            "I guess I got a little carried away... Sorry about that!\p"
            "Uh.. where did I put it...?\p"
            "Don't tell me I dropped it somewhere..."
            )
}

text RivalText2F {
    format("I really don't know what I would've done if I lost it...\p"
    "The professor would've been so mad at me!\p"
    )

}

text RivalText3F { //ATTACKED BY PIDOVE
    format("Uwaaah! Wha... What's going on?!\p"
           "A wild Pokémon?! This is bad! This is really bad!\p"
           "{PLAYER}{KUN}, we have to use the Poké Balls in that bag to protect ourselves!\p"
           "Grab one and let's battle them!"
           )
}

text RivalText4F {
    format("Whew... That was a close one!\p"
           "I can't believe we were attacked by wild Pokémon!\p"
           "Good thing you had {STR_VAR_1} to protect you!\p"
           "You see, I don't really like Pokémon...\p"
           "I just wanted a reason to go out and explore the reason.\p"
           "But I guess I'll have to get used to them if I want to be a Trainer.\p"
           "...We should probably head back to Professor Sanzashi's lab now.\p"
           "I'm sure he must be worried about us."
           )
}
