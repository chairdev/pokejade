const OBJ_RIVAL = 1
const OBJ_PIDOVE1 = 3
const OBJ_PIDOVE2 = 4

mapscripts Route501_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
    }
}

script Route501_Trigger_WalkToRivalLeft
{
    lockall

    applymovement(OBJ_EVENT_ID_PLAYER, PlayerMoveToRivalLeft)
    waitmovement(0)

    checkplayergender
    goto_if_eq(VAR_RESULT, MALE, Route501_RivalHolly)
    goto_if_eq(VAR_RESULT, FEMALE, Route501_RivalCaden)
}

script Route501_Trigger_WalkToRivalRight
{
    lockall

    applymovement(OBJ_EVENT_ID_PLAYER, PlayerMoveToRivalRight)
    waitmovement(0)

    checkplayergender
    goto_if_eq(VAR_RESULT, MALE, Route501_RivalHolly)
    goto_if_eq(VAR_RESULT, FEMALE, Route501_RivalCaden)

}

movement PlayerMoveToRivalLeft {
    walk_right
    walk_up * 2
    walk_in_place_faster_right
}

movement PlayerMoveToRivalRight {
    walk_up * 2
    walk_in_place_faster_right
}

script Route501_RivalHolly {
    
    applymovement(OBJ_RIVAL, RivalNoticePlayer)
    waitmovement(0)

    playbgm(MUS_ENCOUNTER_MAY, 0)

    speakername("{RIVAL}")
    msgbox(Route501_Text_MeetRivalText1F)
    closemessage
    
    applymovement(OBJ_RIVAL, RivalLookAround)
    waitmovement(0)

    speakername("{RIVAL}")
    msgbox(format("Oh, there it is!"))
    closemessage
    
    applymovement(OBJ_RIVAL, RivalApproachBag)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerApproachBag)
    waitmovement(1)


    speakername("{RIVAL}")
    msgbox(Route501_Text_MeetRivalText2F)
    closemessage    

    playbgm(MUS_HELP, 0)

    applymovement(OBJ_PIDOVE1, PidoveApproachPlayer)
    applymovement(OBJ_PIDOVE2, PidoveApprachRival)
    waitmovement(0)
    waitmovement(1)

    playmoncry(SPECIES_PIDOVE, CRY_MODE_NORMAL)

    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, ExclemationMark)
    applymovement(OBJ_RIVAL, ExclemationMark)
    waitmovement(1)
    applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
    applymovement(OBJ_RIVAL, FaceUp)

    speakername("{RIVAL}")
    msgbox(Route501_Text_MeetRivalText3F)
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, FaceRight)
    applymovement(OBJ_RIVAL, FaceDown)

    
    setflag(FLAG_SYS_POKEMON_GET)
    fadescreen(FADE_TO_BLACK)
    removeobject(OBJ_PIDOVE1)
    removeobject(OBJ_PIDOVE2)
	special(ChooseStarter)
	waitstate

    bufferleadmonspeciesname(STR_VAR_1)
    speakername("{RIVAL}")
    msgbox(Route501_Text_MeetRivalText4F)

    special(HealPlayerParty)
    setflag(FLAG_CHOSE_STARTER)
    clearflag(FLAG_HIDE_ZIRCON_TOWN_SPEECH_NPCS)
    setvar(VAR_ROUTE301_STATE, 1)
    setvar(VAR_HAWTHORNE_LAB_STATE, 2)

    warp(MAP_EVERWOOD_TOWN_HAWTHORN_RESEARCH_LAB, 6, 5)
}

script Route501_RivalCaden {
    applymovement(OBJ_RIVAL, RivalNoticePlayer)
    waitmovement(0)

    playbgm(MUS_ENCOUNTER_BRENDAN, 0)

    speakername("{RIVAL}")
    msgbox(Route501_Text_MeetRivalText1M)
    closemessage
    
    applymovement(OBJ_RIVAL, RivalLookAround)
    waitmovement(0)

    speakername("{RIVAL}")
    msgbox(format("Oh, there it is!"))
    closemessage
    
    applymovement(OBJ_RIVAL, RivalApproachBag)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerApproachBag)
    waitmovement(1)

    speakername("{RIVAL}")
    msgbox(Route501_Text_MeetRivalText2M)
    closemessage

    playbgm(MUS_HELP, 0)

    applymovement(OBJ_PIDOVE1, PidoveApproachPlayer)
    applymovement(OBJ_PIDOVE2, PidoveApprachRival)
    waitmovement(0)
    waitmovement(1)

    playmoncry(SPECIES_PIDOVE, CRY_MODE_NORMAL)

    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, ExclemationMark)
    applymovement(OBJ_RIVAL, ExclemationMark)
    waitmovement(1)
    applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
    applymovement(OBJ_RIVAL, FaceUp)

    speakername("{RIVAL}")
    msgbox(Route501_Text_MeetRivalText3M)
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, FaceRight)
    applymovement(OBJ_RIVAL, FaceDown)

    setflag(FLAG_SYS_POKEMON_GET)
    fadescreen(FADE_TO_BLACK)
    removeobject(OBJ_PIDOVE1)
    removeobject(OBJ_PIDOVE2)
	special(ChooseStarter)
	waitstate

    bufferleadmonspeciesname(STR_VAR_1)
     speakername("{RIVAL}")
    msgbox(Route501_Text_MeetRivalText4M)

    special(HealPlayerParty)
    setflag(FLAG_CHOSE_STARTER)
    clearflag(FLAG_HIDE_ZIRCON_TOWN_SPEECH_NPCS)
    setvar(VAR_ROUTE301_STATE, 1)
    setvar(VAR_HAWTHORNE_LAB_STATE, 2)

    warp(MAP_EVERWOOD_TOWN_HAWTHORN_RESEARCH_LAB, 6, 5)
}

movement RivalNoticePlayer {
    emote_exclamation_mark
    walk_in_place_faster_left
}

movement RivalLookAround {
    walk_in_place_up
    walk_in_place_down
    walk_in_place_right
    walk_in_place_faster_right
    emote_exclamation_mark
}

movement RivalApproachBag {
    walk_fast_right
    walk_fast_up
    walk_fast_right * 3
    walk_in_place_faster_down
}

movement PlayerApproachBag {
    walk_right * 4
    walk_in_place_faster_right
}

movement PidoveApproachPlayer {
    walk_faster_down * 6
    face_right
}

movement PidoveApprachRival {
    walk_faster_down* 4
}

movement ExclemationMark {
    emote_exclamation_mark
}

movement FaceUp {
    walk_in_place_faster_up
}

movement FaceDown {
    walk_in_place_faster_down
}

movement FaceLeft {
    walk_in_place_faster_left
}

movement FaceRight {
    walk_in_place_faster_right
}

text Route501_Text_MeetRivalText1F { //female rival
    format("Huh? Oh!\p"
            "You must be {PLAYER}{KUN}, right?\p"
            "I'm {RIVAL}, your neighbor!\p"
            "Professor Hawthorne asked me to bring you something, but when I stopped by, your mom said you were asleep.\p"
            "So I went for a walk; Everwood's so small that I just had to do something.\p"
            "But now that I think of it... I might have dropped it somewhere...\p"
            "Oh no, this isn't good."
            )
}

text Route501_Text_MeetRivalText2F {
    format( "Honestly, I'd be so lost if I can't find it.\p"
            "The professor would be pretty disappointed in me."

    )

}

text Route501_Text_MeetRivalText3F {
    format( "Eek! A wild Pokémon! This is bad, really bad!\p"
            "We need to use the Poké Balls in the bag to defend ourselves!\p"
            "Let's do this, {PLAYER}!"

           )
}

text Route501_Text_MeetRivalText4F {
    format( "Phew... that was close!\p"
            "I didn't think wild Pokémon would just attack us like that!\p"
            "Good thing you had {STR_VAR_1} to protect you.\p"
            "To be honest, I was never really into Pokémon...\p"
            "But I think I'll have to get used to them if I want to see what's beyond Everwood.\p"
            "Come on, let's go back to Professor Hawthorne's lab.\p"
            "He's probably wondering if we got lost or something."

           )
}

text Route501_Text_MeetRivalText1M { //He's a bit less chatty and more to the point than Holly.
    format("Whoa!\p"
            "Hey, you're {PLAYER}, right?\p"
            "I'm {RIVAL}, your neighbor.\p"
            "Professor Hawthorne sent me to deliver something to you, but you were still asleep.\p"
            "Couldn't sit around waiting, so I thought I'd explore a bit. Everwood's not exactly exciting, you know?\p"
            "But anyway, now that you're here, let's check that bag...\p"
            "Huh... weird. Thought I had it right here."

            )
           
}

text Route501_Text_MeetRivalText2M {
    format( "Unreal. I can't believe I lost it...\p"
            "Professor Hawthorne's gonna let me have it when he finds out."

            )
}

text Route501_Text_MeetRivalText3M {
    format( "What the--?! Wild Pokémon?!\p"
            "This isn't good. Alright, grab a Poké Ball from the bag!\p"
            "Let's teach these things a lesson."

           )
}

text Route501_Text_MeetRivalText4M {
    format( "Whew... that was intense!\p"
            "I wasn't expecting wild Pokémon to go after us like that!\p"
            "Guess it's good you had {STR_VAR_1} backing you up.\p"
            "Not gonna lie, I don't exactly see myself as a Pokémon fanatic.\p"
            "But hey, maybe they'll help me get out of here and find some excitement.\p"
            "Anyway, we should probably head back to Professor Hawthorne's lab.\p"
            "He's probably wondering what's taking so long."
           )
}

script Route501_EventScript_SkwovetGirl {
    lock
    faceplayer

    if(flag(FLAG_RECEIVED_POTION_ROUTE_501))
    {
        msgbox(Route501_Text_SkwovetGirl3)
    }
    else
    {
        msgbox(Route501_Text_SkwovetGirl1)
        giveitem(ITEM_ORAN_BERRY, 1)
        msgbox(Route501_Text_SkwovetGirl2)

        setflag(FLAG_RECEIVED_POTION_ROUTE_501)
    }

    release
}

text Route501_Text_SkwovetGirl1 {
    format( "Oh! I'm not a Trainer, don't worry!\p"
            "I'm just here on to watch the Skwovet!\p"
            "It's so funny how they stuff their cheeks full of Berries!\p"
            "Oh! Speaking of Berries, did you know that Skwovet has the ability Cheek Pouch?\p"
            "It's a really useful ability that restores some of its HP when it eats any Berry.\p"
            "I have a few extra Berries, so I'll give you one to try out!"
        )
}

text Route501_Text_SkwovetGirl2 {
    format("I hope you and your Pokémon enjoy the Berries!")
}

text Route501_Text_SkwovetGirl3 {
    format("Skwovet are just too cute when they're eating!\nI could watch them all day!")
}

script Route501_EventScript_MartEmployee {
    lock
    faceplayer

    if(!flag(FLAG_RECEIVED_POTION_ROUTE501))
    {
        msgbox(Route501_Text_MartEmployee1)
        giveitem(ITEM_POTION, 5)

        setflag(FLAG_RECEIVED_POTION_ROUTE501)
    }

    msgbox(Route501_Text_MartEmployee2)
    release
}

text Route501_Text_MartEmployee1 {
    format("Hello! Are you a Pokémon Trainer?\p"
           "We're giving away free Potions to all Trainers who pass through here as a promotion!\p"
           "Please take them as a gift from the Baycrest City Poké Mart!"
           )
}

text Route501_Text_MartEmployee2 {
    format("We hope you'll continue to shop with us in the future!\p"
           "We have a wide selection of items that will help you on your journey!")
}

script Route501_EventScript_Boy { //he's a joke on the pokemon npc thats like did you know that pokemon can be found in the grass?
    lock
    faceplayer
    msgbox(Route501_Text_Boy)
    release
}

text Route501_Text_Boy {
    format("Did you know that Pokémon can be found in the tall grass?\p"
           "I know, it's a shocker!\nLike, who doesn't know that?")
}

script Route501_EventScript_Michael {
    trainerbattle_single(TRAINER_MICHAEL, Route501_Text_MichaelIntro, Route501_Text_MichaelDefeated)
    msgbox(Route102_Text_MichaelPostBattle, MSGBOX_AUTOCLOSE)
    waitmessage
    end
}

text Route501_Text_MichaelIntro {
    format("We've locked eyes! That means it's time for a battle!")
}

text Route501_Text_MichaelDefeated {
    format("You're so strong!")
}

text Route102_Text_MichaelPostBattle {
    format("You're really good at this! I hope we can battle again sometime!")
}

script Route501_Sign_Route {
    lock
    msgbox(Route501_Text_Sign)
    release
}

text Route501_Text_Sign {
    format("Route 301\n{RIGHT_ARROW} Baycrest City\p{DOWN_ARROW} Everwood Town")
}