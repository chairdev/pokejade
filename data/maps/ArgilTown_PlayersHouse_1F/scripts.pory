const OBJ_MOM = 1

mapscripts ArgilTown_PlayersHouse_1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ARGIL_TOWN_STATE, 0: ArgilTown_PlayersHouse_1F_MapScript_MeetMom
        VAR_ROUTE301_STATE, 2: ArgilTown_PlayersHouse_1F_MapScript_MomErrand
    ]
}

script ArgilTown_PlayersHouse_1F_MapScript_MomErrand {
    lockall

    playse(SE_PIN)
    applymovement(OBJ_MOM, Common_Movement_ExclamationMark)
    waitse
    waitmovement(0)
    applymovement(OBJ_MOM, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(0)

    playbgm(MUS_FOLLOW_ME, 0)

    speakername("Mom")
    msgbox(ArgilTown_PlayersHouse_1F_Text_Errand_Mom1)
    closemessage

    applymovement(OBJ_MOM, Common_Movement_WalkInPlaceFasterLeft)
    applymovement(OBJ_EVENT_ID_PLAYER, ArgilTown_PlayersHouse_1F_Movement_Errand_ApproachMom)
    waitmovement(0)

    speakername("Mom")
    msgbox(ArgilTown_PlayersHouse_1F_Text_Errand_Mom2)
    closemessage

    playfanfare(MUS_OBTAIN_ITEM)
    closemessage
    msgbox(format("You put on the Running Shoes!"))
    waitfanfare

    setflag(FLAG_RECEIVED_RUNNING_SHOES)
    setflag(FLAG_SYS_B_DASH)
    setvar(VAR_ARGIL_TOWN_STATE, 3)
    setvar(VAR_ROUTE301_STATE, 3)


    msgbox(ArgilTown_PlayersHouse_1F_Text_Errand_Mom3)

    fadedefaultbgm

    releaseall
}

movement ArgilTown_PlayersHouse_1F_Movement_Errand_ApproachMom {
    walk_up * 3
    walk_right * 4
    delay_16 * 2
    walk_in_place_faster_right
    delay_16
}


text ArgilTown_PlayersHouse_1F_Text_Errand_Mom1 {
    format("Oh, back already?\nHow was your new neighbor?\p"
           "I hope you two hit it off!\nIt's always nice to have a friend nearby.")
}

text ArgilTown_PlayersHouse_1F_Text_Errand_Mom2 {
    format("What? You met Professor Hawthorne?\nThat's wonderful!\p"
           "Is that why you have that cute little Pokémon with you?\p"
           "...Ah, so you're already helping out! That's just like you.\p"
           "You know, this reminds me of when I used to travel for Pokémon Contests... but that's a story for another time.\p"
           "Before you go, I have something for you! I found it while I was unpacking yesterday.")
}

text ArgilTown_PlayersHouse_1F_Text_Errand_Mom3 {
    format("These will help you get around faster--just hold the B Button while walking, and you'll be zooming around in no time!\p"
           "I'll be here if you need me, sweetie.\nGood luck on your errand!")
}




script ArgilTown_PlayersHouse_1F_MapScript_MeetMom {
    lockall
    setrespawn(HEAL_LOCATION_ARGIL_TOWN)
    playbgm(MUS_FOLLOW_ME, 0)
    applymovement(OBJ_MOM, ArgilTown_PlayersHouse_1F_Movement_MomApproachPlayer1)
    waitmovement(0)
    
    speakername("Mom")
    msgbox(ArgilTown_PlayersHouse_1F_Text_MeetMom)
    closemessage
    
    fadedefaultbgm
    applymovement(OBJ_MOM, MomMoveBack)
    waitmovement(0)

    setvar(VAR_ARGIL_TOWN_STATE, 1)
    
    
    releaseall
}

text ArgilTown_PlayersHouse_1F_Text_MeetMom {
    format("Oh, good morning, sweetie!\nUp already?\p"
           "You must be full of energy today--I thought you'd be sleeping in after all the unpacking yesterday.\p"
           "It's strange, isn't it?\nEverything feels so new here... different from Hoenn.\p"
           "Oh! Before I forget, the neighbors across the street stopped by yesterday.\p"
           "They said they have a child about your age.\p"
           "Why don't you go over and introduce yourself?\lIt might be nice to make a new friend!"
           )
              
}

movement ArgilTown_PlayersHouse_1F_Movement_MomApproachPlayer1 {
    walk_up
    walk_right * 2
    walk_in_place_faster_up
}

movement MomMoveBack {
    walk_left * 2
    walk_down
    walk_in_place_faster_right
}

script ArgilTown_PlayersHouse_1F_EventScript_Mom {
    lockall
    faceplayer

    if(var(VAR_ARGIL_TOWN_STATE) < 2)
    {
        speakername("Mom")
        msgbox(format("Take your time exploring, too.\nIt's a beautiful day, and you never know what you might find."))
        closemessage
    }
    else
    {
        msgbox(ArgilTown_PlayersHouse_1F_Text_MomHealsParty)
        closemessage
        call(Common_EventScript_OutOfCenterPartyHeal)
        incrementgamestat(GAME_STAT_RESTED_AT_HOME)
        msgbox(ArgilTown_PlayersHouse_1F_Text_GoGetEm)
        release
    }
        
    

    releaseall
}

text ArgilTown_PlayersHouse_1F_Text_MomHealsParty {
    format("Welcome home, {PLAYER}!\p"
           "You look tired.\nWhy don't you rest a bit?")
}

text ArgilTown_PlayersHouse_1F_Text_GoGetEm { //right after healing
    format("There! You look like you're ready to take on the world!\p"
           "Go get 'em, {PLAYER}!"
           )
}

script ArgilTown_PlayersHouse_1F_EventScript_Delcatty {
    lock
    faceplayer
    playmoncry(SPECIES_DELCATTY, CRY_MODE_NORMAL)
    msgbox(format("Mreeeow!"))
    waitmoncry
    release
}

