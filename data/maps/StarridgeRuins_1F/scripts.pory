const OBJ_GRUNT_A = 3
const OBJ_GRUNT_B = 2
const OBJ_RIVAL = 4

mapscripts StarridgeRuins_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)       
    }

    MAP_SCRIPT_ON_LOAD {
         if(var(VAR_SPINEL_CITY_STATE) == 3) {
            setobjectxy(OBJ_RIVAL, 7, 8)
        }
     }

}

script StarridgeRuins_1F_EventScript_Grunts {
    lock
    faceplayer

    fadescreen(FADE_TO_BLACK)
    setobjectxy(OBJ_EVENT_ID_PLAYER, 7, 7)
    fadescreen(FADE_FROM_BLACK)

    applymovement(OBJ_GRUNT_A, StarridgeRuins_1F_Movement_GruntA_Approach)
    waitmovement(OBJ_GRUNT_A)

    msgbox(StarridgeRuins_1F_Text_Grunts_GruntA_1)
    closemessage

    if(var(VAR_SPINEL_CITY_STATE) == 2) {
        playbgm(MUS_ENCOUNTER_AQUA, FALSE)
        applymovement(OBJ_GRUNT_B, StarridgeRuins_1F_Movement_GruntB_Exclamation)
        waitmovement(OBJ_GRUNT_B)

        msgbox(StarridgeRuins_1F_Text_Grunts_GruntB_1)
        closemessage

        applymovement(OBJ_GRUNT_B, StarridgeRuins_1F_Movement_GruntB_Approach)
        waitmovement(OBJ_GRUNT_B)

        setobjectxy(OBJ_RIVAL, 4, 12)
        applymovement(OBJ_RIVAL, StarridgeRuins_1F_Movement_Rival_WalkIn2)  
        waitmovement(OBJ_RIVAL)

        checkplayergender
        if(var(VAR_RESULT) == MALE) {
            speakername("{RIVAL}")
            msgbox(StarridgeRuins_1F_Text_Grunts_Holly_Entrance)
            closemessage

            msgbox(StarridgeRuins_1F_Text_Grunts_GruntA_Response)

            speakername("{RIVAL}")
            msgbox(StarridgeRuins_1F_Text_Grunts_Holly_Determination)
            closemessage

            choose_mons
            setdynamicaifunc(AI_TagBattlePreferFoe)

            switch(var(VAR_STARTER_MON)) {
            case 0:
                multi_2_vs_2(TRAINER_GRUNT_STARRIDGE_RUINS_1, StarridgeRuins_1F_Text_Grunts_GruntA_Defeated, TRAINER_GRUNT_STARRIDGE_RUINS_2, StarridgeRuins_1F_Text_Grunts_GruntB_Defeated, PARTNER_HOLLY_STARRIDGE_RUINS_TURTWIG)
                break
            case 1:
                multi_2_vs_2(TRAINER_GRUNT_STARRIDGE_RUINS_1, StarridgeRuins_1F_Text_Grunts_GruntA_Defeated, TRAINER_GRUNT_STARRIDGE_RUINS_2, StarridgeRuins_1F_Text_Grunts_GruntB_Defeated, PARTNER_HOLLY_STARRIDGE_RUINS_FENNEKIN)
                break
            case 2:
                multi_2_vs_2(TRAINER_GRUNT_STARRIDGE_RUINS_1, StarridgeRuins_1F_Text_Grunts_GruntA_Defeated, TRAINER_GRUNT_STARRIDGE_RUINS_2, StarridgeRuins_1F_Text_Grunts_GruntB_Defeated, PARTNER_HOLLY_STARRIDGE_RUINS_QUAXLY)
                break
            }

            speakername("{RIVAL}")
            msgbox(StarridgeRuins_1F_Text_Holly_Response)
            closemessage
        }
        else
        {
            speakername("{RIVAL}")
            msgbox(StarridgeRuins_1F_Text_Grunts_Caden_Entrance)
            closemessage

            msgbox(StarridgeRuins_1F_Text_Grunts_GruntA_Response)

            speakername("{RIVAL}")
            msgbox(StarridgeRuins_1F_Text_Grunts_Caden_Taunt)
            closemessage    

            choose_mons
            setdynamicaifunc(AI_TagBattlePreferFoe)

            switch(var(VAR_STARTER_MON)) {
            case 0:
                multi_2_vs_2(TRAINER_GRUNT_STARRIDGE_RUINS_1, StarridgeRuins_1F_Text_Grunts_GruntA_Defeated, TRAINER_GRUNT_STARRIDGE_RUINS_2, StarridgeRuins_1F_Text_Grunts_GruntB_Defeated, PARTNER_CADEN_STARRIDGE_RUINS_TURTWIG)
                break
            case 1:
                multi_2_vs_2(TRAINER_GRUNT_STARRIDGE_RUINS_1, StarridgeRuins_1F_Text_Grunts_GruntA_Defeated, TRAINER_GRUNT_STARRIDGE_RUINS_2, StarridgeRuins_1F_Text_Grunts_GruntB_Defeated, PARTNER_CADEN_STARRIDGE_RUINS_FENNEKIN)
                break
            case 2:
                multi_2_vs_2(TRAINER_GRUNT_STARRIDGE_RUINS_1, StarridgeRuins_1F_Text_Grunts_GruntA_Defeated, TRAINER_GRUNT_STARRIDGE_RUINS_2, StarridgeRuins_1F_Text_Grunts_GruntB_Defeated, PARTNER_CADEN_STARRIDGE_RUINS_QUAXLY)
                break
            }

            speakername("{RIVAL}")
            msgbox(StarridgeRuins_1F_Text_Caden_Response)
        }

        closemessage
        msgbox(StarridgeRuins_1F_Text_GruntA_Retreat)
        closemessage
        fadescreen(FADE_TO_BLACK)
        setflag(FLAG_HIDE_ROUTE_304_ETERNITY_GRUNT)
        removeobject(OBJ_GRUNT_A)
        removeobject(OBJ_GRUNT_B)
        setvar(VAR_SPINEL_CITY_STATE, 3)
        fadescreen(FADE_FROM_BLACK)
    }
    else {
        applymovement(OBJ_GRUNT_A, StarridgeRuins_1F_Movement_GruntA_Push)
        applymovement(OBJ_EVENT_ID_PLAYER, StarridgeRuins_1F_Movement_Player_Push)
        waitmovement(OBJ_EVENT_ID_PLAYER)
        applymovement(OBJ_GRUNT_A, StarridgeRuins_1F_Movement_GruntA_WalkBack)
        waitmovement(OBJ_GRUNT_A)
    }
   
    release
}

text StarridgeRuins_1F_Text_Grunts_GruntA_1 {
    format("Can't you see we're busy here?\p"
            "We're trying to find the treasure in this place!\p"
            "Why don't you go mind the business that pays you, huh?")
}

text StarridgeRuins_1F_Text_Grunts_GruntB_1 {
    format("Hey, wait a minute!\p"
            "You're the kid that beat me in that stupid nerd club!\p"
            "I'm not going to let you get in my way again!\p"
            "I've got backup this time, so you're going down!")  
}
text StarridgeRuins_1F_Text_Grunts_Caden_Entrance {
    format("Hey! Did I just hear you threaten my friend?\p"
           "I don't know who you are, but that's not happening on my watch!")
}

text StarridgeRuins_1F_Text_Grunts_GruntA_Response{
    format( "You think you can just stroll in here and stop us?\p" 
            "Well, think again!\p"
            "It's Eternity and beyond for us, kid!\nPrepare to lose!")
}

text StarridgeRuins_1F_Text_Grunts_Caden_Taunt {
    format("Team Eternity, huh? Big talk for a couple of grunts.\p"
           "Let's see if you're as tough as you think!")
}

text StarridgeRuins_1F_Text_Grunts_Holly_Entrance {
    format("Hey, {PLAYER}!\nIs everything okay?\p"
           "It sounds like you're in trouble, and I'm not going to stand by and watch!")
}

text StarridgeRuins_1F_Text_Grunts_Holly_Determination {
    format("I don't know what you two bozos are up to, but we're not letting you push us around!\p"
           "Ready, {PLAYER}? Let's show them what we can do together!")
}

text StarridgeRuins_1F_Text_Grunts_GruntA_Defeated {
    format("No way!\nHow did we lose to a couple of kids?")
}

text StarridgeRuins_1F_Text_Grunts_GruntB_Defeated {
    format("Wuh-oh!\nBoss is not gonna like this...")
}

text StarridgeRuins_1F_Text_Caden_Response {
    format("Ha! Thought you were tough?\p"
           "Guess you can't handle the 'kids' after all!")
}

text StarridgeRuins_1F_Text_Holly_Response {
    format("Looks like you underestimated us.\p"
           "Maybe next time you'll think twice!")
}

text StarridgeRuins_1F_Text_GruntA_Retreat {
    format("Fine! You may have won this time,\p"
           "but Team Eternity doesn't back down that easily!\p"
           "Let's go tell the boss. This isn't over!")
}

movement StarridgeRuins_1F_Movement_GruntA_Approach {
    walk_left
}

movement StarridgeRuins_1F_Movement_GruntA_Push {
    walk_in_place_fast_left
}

movement StarridgeRuins_1F_Movement_GruntA_WalkBack {
    walk_right
    walk_in_place_fast_left
}

movement StarridgeRuins_1F_Movement_Player_Push {
    lock_facing_direction
    jump_left
    face_right
    unlock_facing_direction
}

movement StarridgeRuins_1F_Movement_GruntB_Exclamation {
    emote_exclamation_mark
    walk_in_place_faster_left
}

movement StarridgeRuins_1F_Movement_GruntB_Approach {
    walk_left * 2
}

movement StarridgeRuins_1F_Movement_Rival_WalkIn1 {
    walk_fast_up * 5
    walk_fast_right * 3
}

movement StarridgeRuins_1F_Movement_Rival_WalkIn2 {
    walk_fast_up * 4
    walk_fast_right * 3
}

script StarridgeRuins_1F_EventScript_Rival {
    lock
    faceplayer

    speakername("{RIVAL}")
    msgbox(StarridgeRuins_1F_Text_Rival)

    release
}

text StarridgeRuins_1F_Text_Rival {
    format("Let's keep moving, {PLAYER}!\p"
           "We need to find out what's going on here!")
}