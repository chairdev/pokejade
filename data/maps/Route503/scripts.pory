const OBJ_RIVAL = 9
const OBJ_ASTER = 8
const OBJ_DELCATTY = 16
const OBJ_MOM = 15

//VAR_ROUTE503_STATE
//0: Aster Fight
//1: Receive PokeNav
//2: Done

mapscripts Route503_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)

        setflag(FLAG_HIDE_ROUTE_503_MOM_DELCATTY)

        if(!flag(FLAG_SAVED_NURSERY_MAN))
        {
            setflag(FLAG_HIDE_ROUTE_503_NURSERY_MAN)
        }
        else
        {
            clearflag(FLAG_HIDE_ROUTE_503_NURSERY_MAN)
        }
    }
}

script Route503_Trigger_MeetAster1 {
    lockall

    speakername("???")
    msgbox(format("What a pathetic excuse for a trainer!\nYou're not even worth my time!"))
    closemessage

    speakername("{RIVAL}")
    msgbox(format("What did you just say?"))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, Route503_Movement_PlayerApproachRival1)
    waitmovement(0)

    goto(Route503_MeetAster)
}

script Route503_Trigger_MeetAster2 {
    lockall

    speakername("???")
    msgbox(format("What a pathetic excuse for a trainer!\nYou're not even worth my time!"))
    closemessage

    speakername("{RIVAL}")
    msgbox(format("What did you just say?"))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, Route503_Movement_PlayerApproachRival2)
    waitmovement(0)

    goto(Route503_MeetAster)
}

movement Route503_Movement_PlayerApproachRival1 {
    walk_right * 3
}

movement Route503_Movement_PlayerApproachRival2 {
    walk_right * 1
    walk_up
    walk_right * 2
}

script Route503_MeetAster { //the rival and aster just faught, and the rival lost
    applymovement(OBJ_RIVAL, Route503_RivalFaceUp)
    waitmovement(0)

    speakername("{RIVAL}")
    msgbox(Route503_Text_MeetAster_Rival1)

    applymovement(OBJ_RIVAL, Route503_RivalFaceRight)

    speakername("???")
    msgbox(Route503_Text_MeetAster_Aster1)

    speakername("{RIVAL}")
    msgbox(Route503_Text_MeetAster_Rival2, MSGBOX_YESNO)

    applymovement(OBJ_RIVAL, Route503_RivalFaceUp)

    if (var(VAR_RESULT) == NO) {
        while {
            msgbox(Route503_Text_MeetAster_Rival3, MSGBOX_YESNO)
            if (var(VAR_RESULT)  == YES) {
                break
            }
        }
    }

    msgbox(format("That's what I thought!"))

    applymovement(OBJ_RIVAL, Route503_RivalFaceRight)

    speakername("Aster")
    msgbox(Route503_Text_MeetAster_Aster2)

    trainerbattle_no_intro(TRAINER_ASTER_ROUTE_503, Route503_Text_AsterDefeated)

    applymovement(OBJ_ASTER, Route503_AsterStepBack)
    waitmovement(0)

    speakername("Aster")
    msgbox(Route503_Text_MeetAster_Aster3)
    closemessage

    applymovement(OBJ_ASTER, Route503_AsterLeave)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Route503_RivalFaceUp)
    applymovement(OBJ_RIVAL, Route503_RivalFaceUp)
    waitmovement(0)
    
    waitmovement(0)
    removeobject(OBJ_ASTER)

    speakername("Aster")
    msgbox(format("Out of my way, stupid Sudowoodo!"))
    closemessage

    playmoncry(SPECIES_SUDOWOODO, CRY_MODE_NORMAL)
    msgbox("Sudo!\nWoo...")
    waitmoncry

    applymovement(OBJ_EVENT_ID_PLAYER, Route503_Movement_PlayerFaceDown)

    speakername("{RIVAL}")
    msgbox(Route503_Text_MeetAster_Rival4)
    closemessage

    fadescreen(FADE_TO_BLACK)
    special(HealPlayerParty)
    playse(SE_USE_ITEM)
    msgbox(format("{RIVAL} healed your Pokémon!"))
    closemessage
    waitse
    fadescreen(FADE_FROM_BLACK)

    speakername("{RIVAL}")
    msgbox(Route503_Text_MeetAster_Rival5)

    switch(var(VAR_STARTER_MON)) {
        case 0:
            giveitem(ITEM_MIRACLE_SEED)
            break
        case 1:
            giveitem(ITEM_CHARCOAL)
            break
        case 2:
            giveitem(ITEM_MYSTIC_WATER)
            break
    }

    speakername("{RIVAL}")
    msgbox(Route503_Text_MeetAster_Rival6)
    closemessage
    
    applymovement(OBJ_RIVAL, Route503_RivalLeave)
    waitmovement(0)
    removeobject(OBJ_RIVAL)

    setvar(VAR_ROUTE503_STATE, 1)
    setflag(FLAG_ROUTE_503_METASTER)

    releaseall
}

movement Route503_Movement_PlayerFaceDown {
    walk_in_place_faster_down
}

movement Route503_RivalFaceUp {
    walk_in_place_faster_up
}

movement Route503_RivalFaceRight {
    walk_in_place_faster_right
}

movement Route503_AsterStepBack {
    lock_facing_direction
    walk_right
    unlock_facing_direction
}

movement Route503_AsterLeave {
    walk_left
    walk_up * 6
}

movement Route503_RivalLeave {
    walk_right * 7
    walk_down * 4
}

text Route503_Text_MeetAster_Rival1 {
    format("Oh, it's you, {PLAYER}!\nI'm glad you're here!\p"
            "Listen, I was busy working on my Pokédex when this guy suddenly challenged me to a battle!\p"
            "I didn't want to, but he wouldn't take no for an answer!\p"
            "I tried my best, but I just couldn't beat him!"
    )
}

text Route503_Text_MeetAster_Aster1 {
    format( "Hah! You really think you've got what it takes?\p"
            "Weak trainers like you are better off cheering from the sidelines.\p"
            "A real trainer has strength and skill, and that's why I'm the best around.\p"
            "I'm Aster.\nTry to keep up, if you can!"
    )
}

text Route503_Text_MeetAster_Rival2 {
    format("Gah! You're so annoying!\nYou know what? I'm going to make you pay for this!\p"
            "{PLAYER}, go and show him what you're made of!\n"
    )
}

text Route503_Text_MeetAster_Rival3 {
    format("What's that, {PLAYER}?\nI can't hear you!\p"
            "You're going to battle him.\n...Right?"
    )
}

text Route503_Text_MeetAster_Aster2 {
    format("Oh, so you think you're tough?\p"
            "You might be better than your friend here, but that's not saying much!\p"
            "Let me show you what a real battle looks like."
    )
}

text Route503_Text_AsterDefeated {
    format("What? No way!\nLosing to you? That shouldn't happen!")
}

text Route503_Text_MeetAster_Aster3 {
    format("These Pokémon just didn't cut it!\nI need stronger ones, that's all.\p"
            "Next time, you'll see what I can really do."
    )
}

text Route503_Text_MeetAster_Rival4 {
    format("...What a jerk!\nI can't believe I lost to him!\p"
            "Did you see how he treated his Pokémon?\nHe's nothing but a bully!\p"
            "But, wow! You're amazing, {PLAYER}!\nYou really showed him!\p"
            "Oh, I almost forgot!\nYour Pokémon must be tired after that battle!\p"
            "I'll heal them up for you!"
    )
}

text Route503_Text_MeetAster_Rival5 {
    format( "There we go! All healed up!\p"
            "...I'm still not sure about this whole battling thing, but I'm glad I have you to help me out!\p"
            "Oh, and I almost forgot! I found this while I was out looking for Pokémon!\p"
            "I think it's a held item! You should take it! I'm sure you'll put it to good use!"
    )
}

text Route503_Text_MeetAster_Rival6 {
    format("OK! That's enough chit-chat!\nLet's get back to training!\p"
            "I'll see you later, {PLAYER}! Let's battle again soon!"
    )
}


script Route503_Trigger_GetPokeNav1 {
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, Route503_Movement_PlayerFaceDown)
    clearflag(FLAG_HIDE_ROUTE_503_MOM_DELCATTY)

    playmoncry(SPECIES_DELCATTY, CRY_MODE_NORMAL)
    msgbox("Mrow!")
    waitmoncry
    closemessage
    
    addobject(OBJ_MOM, MAP_ROUTE503) 
    addobject(OBJ_DELCATTY, MAP_ROUTE503)
    applymovement(OBJ_DELCATTY, Route503_Movement_DelcattyApproachPlayer1)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Route503_Movement_FaceLeft)

    speakername("???")
    msgbox("{PLAYER}! Wait... up!")
    closemessage

    playbgm(MUS_FOLLOW_ME, FALSE)
    
    applymovement(OBJ_EVENT_ID_PLAYER, Route503_Movement_PlayerFaceDown)
    applymovement(OBJ_MOM, Route503_Movement_MomApproachPlayer1)
    waitmovement(0)

    goto(Route503_GetPokeNav)
}

script Route503_Trigger_GetPokeNav2 {
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, Route503_Movement_PlayerFaceDown)

    playmoncry(SPECIES_DELCATTY, CRY_MODE_NORMAL)
    msgbox("Mrow!")
    waitmoncry
    closemessage

    addobject(OBJ_MOM, MAP_ROUTE503) 
    addobject(OBJ_DELCATTY, MAP_ROUTE503)
    applymovement(OBJ_DELCATTY, Route503_Movement_DelcattyApproachPlayer2)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Route503_Movement_FaceLeft)

    speakername("???")
    msgbox("{PLAYER}! Wait... up!")
    closemessage

    playbgm(MUS_FOLLOW_ME, FALSE)
    
    applymovement(OBJ_EVENT_ID_PLAYER, Route503_Movement_PlayerFaceDown)
    applymovement(OBJ_MOM, Route503_Movement_MomApproachPlayer2)
    waitmovement(0)

    goto(Route503_GetPokeNav)
}

script Route503_GetPokeNav {
    speakername("Mom")
    msgbox(Route503_Text_GetPokeNav_Mom1)
    closemessage

    playfanfare(MUS_OBTAIN_ITEM)
	msgbox("You received the PokéNav!")
    waitfanfare
    setflag(FLAG_SYS_POKENAV_GET)
    setflag(FLAG_ADDED_MATCH_CALL_TO_POKENAV)
    setflag(FLAG_HAS_MATCH_CALL)

    speakername("Mom")
    msgbox(Route503_Text_GetPokeNav_Mom2)
    closemessage

	delay(30)
	playfanfare(MUS_REGISTER_MATCH_CALL)
	msgbox(format("Registered Mom in the PokéNav!"))
	waitfanfare
	closemessage
	delay(30)
	setflag(FLAG_ENABLE_MOM_MATCH_CALL)

    speakername("Mom")
    msgbox(Route503_Text_GetPokeNav_Mom3)
    closemessage

    setvar(VAR_ROUTE503_STATE, 2)

    fadedefaultbgm
    fadescreen(FADE_TO_BLACK)
    removeobject(OBJ_MOM)
    removeobject(OBJ_DELCATTY)
    setflag(FLAG_HIDE_ROUTE_503_MOM_DELCATTY)
    fadescreen(FADE_FROM_BLACK)

    releaseall
}

movement Route503_Movement_FaceLeft {
    walk_in_place_faster_left
}

movement Route503_Movement_DelcattyApproachPlayer1 {
    walk_fast_right * 7
    walk_fast_up * 5
    walk_in_place_faster_down
}

movement Route503_Movement_DelcattyApproachPlayer2 {
    walk_fast_right * 8
    walk_fast_up * 5
    walk_in_place_faster_down
}

movement Route503_Movement_MomApproachPlayer1 {
    walk_right * 8
    walk_fast_up * 4
}

movement Route503_Movement_MomApproachPlayer2 {
    walk_right * 9
    walk_up * 4
}

text Route503_Text_GetPokeNav_Mom1 {
    format( "{PLAYER}!\nThank goodness I caught you in time!\p"
            "I knew you'd be heading into Kismet Forest soon, and I wanted to make sure you had something important before you went any further.\p"
            "Here, I got this for you--a Pokémon Navigator, or PokéNav for short!"
    )
}

text Route503_Text_GetPokeNav_Mom2 { //register herself to the pokenav
    format( "I ordered it straight from the Devon Corporation in Hoenn!\p"
            "It's a handy device that has a map, a phone, and even the ability to check the condition of your Pokémon!\p"
            "I thought it would be perfect for you, especially since you're just starting out on your journey!\p"
            "Here, I'll register myself in your PokéNav.\nAnd don't hesitate to call me if you need anything!"

    )
}

text Route503_Text_GetPokeNav_Mom3 {
    format("{PLAYER}...\p"
            "Be careful out there, okay? I know you'll do great!\p"
            "And remember, no matter how far you go, you're never alone. We're all rooting for you back home!"

    )
}


script Route503_EventScript_NurseryLady {
    lock
    faceplayer
    msgbox(Route503_Text_NurseryLady)
    release
}

text Route503_Text_NurseryLady { //they're having trouble with the nursery since the nursery man has gone to the pokemon fan club in stemford town
    format( "I'm sorry for blocking the path, Toxel and Pichu are having a playdate, and I don't want to interrupt them!\p"
            "The Nursery Man is supposed to be here to help me, but he's gone to the Pokémon Fan Club in Stemford Town and hasn't come back yet.\p"
            "I'm worried he might have lost track of time. He's always so busy talking about Pokémon that he forgets everything else!\p"
            "If you see him, could you please tell him to come back here right away?"
            )
    
}

script Route503_EventScript_Toxel {
    lock
    faceplayer
    playmoncry(SPECIES_TOXEL, CRY_MODE_NORMAL)
    msgbox(format("Ssson!"))
    waitmoncry
    release
}

script Route503_EventScript_Sudowoodo {
    lock
    faceplayer
    playmoncry(SPECIES_SUDOWOODO, CRY_MODE_NORMAL)
    msgbox(format("Sudooo!"))
    waitmoncry
    release
}

script Route503_EventScript_Pichu {
    lock
    faceplayer
    playmoncry(SPECIES_PICHU, CRY_MODE_NORMAL)
    msgbox(format("Pichuuu!"))
    waitmoncry
    release
}

script Route503_EventScript_FatGuy {
    lock
    faceplayer
    msgbox(Route503_Text_FatGuy)
    release   
}

text Route503_Text_FatGuy {
    format( "Every Route has one or two rare Pokémon that are hard to find!\p"
            "If you're patient and keep looking, you might just find one!\p"
    )
    
}

// Route 303 Trainers
script Route503_EventScript_CamperAndrew {
    trainerbattle_single(TRAINER_ANDREW, Route503_Text_CamperAndrewIntro, Route503_Text_CamperAndrewDefeated)
    msgbox(Route503_Text_CamperAndrewPostBattle, MSGBOX_AUTOCLOSE)
    waitmessage
    end
}

text Route503_Text_CamperAndrewIntro {
    format("I love camping out in the wild and battling with my Pokémon!")
}

text Route503_Text_CamperAndrewDefeated {
    format("Camping out in the wild is fun, but battling with my Pokémon is even better!")
}

text Route503_Text_CamperAndrewPostBattle {
    format("Now to set up camp and relax for a bit!")
}

script Route503_EventScript_YoungsterEric {
    trainerbattle_single(TRAINER_ERIC, Route503_Text_YoungsterEricIntro, Route503_Text_YoungsterEricDefeated)
    msgbox(Route503_Text_YoungsterEricPostBattle, MSGBOX_AUTOCLOSE)
    waitmessage
    end
}

text Route503_Text_YoungsterEricIntro {
    format( "1, 2, 3! 1, 2, 3!\nI can beat you in 1, 2, 3 moves!")
}

text Route503_Text_YoungsterEricDefeated {
    format("1, 2, 3!\nI lost in 1, 2, 3 moves!")
}

text Route503_Text_YoungsterEricPostBattle {
    format("1, 2, 3! 1, 2, 3!\nNeed to focus!")
}

script Route503_EventScript_PicnickerMargaret {
    trainerbattle_single(TRAINER_MARGARET, Route503_Text_PicnickerMargaretIntro, Route503_Text_PicnickerMargaretDefeated)
    msgbox(Route503_Text_PicnickerMargaretPostBattle, MSGBOX_AUTOCLOSE)
    waitmessage
    end
}

text Route503_Text_PicnickerMargaretIntro {
    format("Argh! You're getting in the way of my picnic!")
}

text Route503_Text_PicnickerMargaretDefeated {
    format("Now my picnic is ruined...")
}

text Route503_Text_PicnickerMargaretPostBattle {
    format("I'll just have to find a new spot for my picnic!")
}

script Route503_EventScript_RichboyCarl {
    trainerbattle_single(TRAINER_CARL_1, Route503_Text_RichboyCarlIntro, Route503_Text_RichboyCarlDefeated)
    msgbox(Route503_Text_RichboyCarlPostBattle, MSGBOX_AUTOCLOSE)
    waitmessage
    end
}

text Route503_Text_RichboyCarlIntro {
    format("I love to come here and watch all the poor people struggle to train their Pokémon!")
}

text Route503_Text_RichboyCarlDefeated {
    format("...Can I pay you to forget that ever happened?")
}

text Route503_Text_RichboyCarlPostBattle {
    format("I may have lost, but I'm still rich!")
}