const OBJ_MEGA_EVO_GURU = 1
const OBJ_GEO = 2
const OBJ_RIVAL = 3

mapscripts DolomiteCity_TrainerSchool_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
    }

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_DOLOMITE_CITY_STATE, 0:  DolomiteCity_TrainerSchool_MapScript_DeliverParcel
    ]
}

script DolomiteCity_TrainerSchool_MapScript_DeliverParcel {
    lockall

    applymovement(OBJ_EVENT_ID_PLAYER, DolomiteCity_TrainerSchool_Movement_DeliverParcel_WalkIn)

    msgbox(DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru1)
    closemessage

    call(DolomiteCity_TrainerSchool_MapScript_MegaEvoGuruExclamation)

    msgbox(DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru2)
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, DolomiteCity_TrainerSchool_Movement_DeliverParcel_ApproachMegaEvoGuru)
    waitmovement(OBJ_EVENT_ID_PLAYER)

    applymovement(OBJ_MEGA_EVO_GURU, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)

    msgbox(DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru3)

    call(DolomiteCity_TrainerSchool_MapScript_MegaEvoGuruExclamation)

    msgbox(DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru4)
    giveitem(ITEM_EXP_SHARE)
    setflag(FLAG_EXP_SHARE)
    closemessage

    msgbox(DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru5)
    closemessage

    setvar(VAR_DOLOMITE_CITY_STATE, 1)
    setvar(VAR_HAWTHORNE_LAB_STATE, 3)
    removeitem(ITEM_PARCEL)

    releaseall
}

script DolomiteCity_TrainerSchool_MapScript_MegaEvoGuruExclamation {
    playse(SE_PIN)
    applymovement(OBJ_MEGA_EVO_GURU, Common_Movement_ExclamationMark)
    waitse
    waitmovement(0)
    delay(16)
}

movement DolomiteCity_TrainerSchool_Movement_DeliverParcel_WalkIn {
    walk_slow_up * 4
}

movement DolomiteCity_TrainerSchool_Movement_DeliverParcel_ApproachMegaEvoGuru {
    walk_fast_right * 2
    walk_fast_up * 3
    walk_in_place_faster_left
}

text DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru1 {
    format("--and that is why only certain Pokémon species are capable of Mega Evolution.\p"
           "However, simply possessing a Mega Stone is not enough. The bond between Trainer and Pokémon--")
}

text DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru2 {
    format("Ah, a visitor! Can I help you with something?")
}

text DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru3 {
    format( "Oh? A delivery from Professor Hawthorne?\p"
            "Let me see...")
}

text DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru4 {
    format( "Incredible... These are never before seen Mega Stones!\p"
            "Professor Hawthorne must trust you a great deal to handle something so valuable.\p"
            "I must thank you for delivering these to me. Please, take this as a token of my gratitude.")
}

text DolomiteCity_TrainerSchool_Text_DeliverParcel_MegaEvoGuru5 {
    format( "You can turn the Exp. Share on or off by selecting it from your Bag.\p"
            "Please give Professor Hawthorne my regards. I must get back to my research.\p"
            "I hope to see you again soon.")
}

script DolomiteCity_TrainerSchool_EventScript_MegaEvoGuru {
    lock
    faceplayer
    msgbox(DolomiteCity_TrainerSchool_Text_MegaEvoGuru)
    closemessage
    release
}

text DolomiteCity_TrainerSchool_Text_MegaEvoGuru {
    format("Thank you again for delivering these.\pIf you ever wish to learn more about Mega Evolution, my door is always open.")
}

script DolomiteCity_TrainerSchool_EventScript_Geo {
    lockall
    faceplayer

    speakername("???")
    msgbox(DolomiteCity_TrainerSchool_Geo_Text1)
    
    applymovement(OBJ_GEO, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(0)

    speakername("Geo")
    msgbox(DolomiteCity_TrainerSchool_Geo_Text2)
    
    faceplayer

    msgbox(DolomiteCity_TrainerSchool_Geo_Text3)

    dynmultichoice(20, 8, TRUE, 2, 0, DYN_MULTICHOICE_CB_NONE,
    "Absolutely!", "Not yet.")

    if (var(VAR_RESULT) == 0) {
        msgbox(DolomiteCity_TrainerSchool_Geo_Text_Accept)
        closemessage

        playse(SE_PIN)
        applymovement(OBJ_RIVAL, Common_Movement_ExclamationMark)
        waitse
        waitmovement(0)
        delay(16)
        
        msgbox(DolomiteCity_TrainerSchool_Geo_Text_RivalIntro)
        closemessage

        checkplayergender
        if(var(VAR_RESULT) == FEMALE)
        {
            playbgm(MUS_ENCOUNTER_BRENDAN, FALSE)
        }
        else
        {
            playbgm(MUS_ENCOUNTER_MAY, FALSE)
        }

        applymovement(OBJ_RIVAL, DolomiteCity_TrainerSchool_Movement_RivalApproachPlayer)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)

        speakername("{RIVAL}")
        if(var(VAR_RESULT) == FEMALE)
        {
            msgbox(DolomiteCity_TrainerSchool_Rival_Text1_Caden)
            closemessage

            switch(var(VAR_STARTER_MON)) {
            case 0:
                trainerbattle_no_intro(TRAINER_CADEN_DOLOMITE_CHESPIN, DolomiteCity_TrainerSchool_CadenDefeated)
                break
            case 1:
                trainerbattle_no_intro(TRAINER_CADEN_DOLOMITE_FENNEKIN, DolomiteCity_TrainerSchool_CadenDefeated)
                break   
            case 2:
                trainerbattle_no_intro(TRAINER_CADEN_DOLOMITE_FROAKIE, DolomiteCity_TrainerSchool_CadenDefeated)
                break
            }
        }
        else
        {
            msgbox(DolomiteCity_TrainerSchool_Rival_Text1_Holly)
            closemessage

            switch(var(VAR_STARTER_MON)) {
            case 0:
                trainerbattle_no_intro(TRAINER_HOLLY_DOLOMITE_CHESPIN, DolomiteCity_TrainerSchool_HollyDefeated)
                break
            case 1:
                trainerbattle_no_intro(TRAINER_HOLLY_DOLOMITE_FENNEKIN, DolomiteCity_TrainerSchool_HollyDefeated)
                break
            case 2:
                trainerbattle_no_intro(TRAINER_HOLLY_DOLOMITE_FROAKIE, DolomiteCity_TrainerSchool_HollyDefeated)
                break
            }
        }

        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)

        speakername("Geo")
        msgbox(DolomiteCity_TrainerSchool_Geo_Text_AfterBattle)
        closemessage

        fadescreen(FADE_TO_BLACK)
        delay(16)
        playse(SE_EXIT)
        removeobject(OBJ_GEO)
        waitse
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
        waitmovement(0)
        delay(16)
        fadescreen(FADE_FROM_BLACK)

        speakername("{RIVAL}")
        msgbox(format("I'm going to train hard and challenge the Dolomite Gym!\p"
                      "You should take this TM as a thank you for helping me out."))
        closemessage
        giveitem(ITEM_TM_SWIFT)

        speakername("{RIVAL}")
        msgbox(format("I'll see you later, {PLAYER}!"))
        closemessage

        applymovement(OBJ_RIVAL, DolomiteCity_TrainerSchool_Movement_RivalLeave)
        waitmovement(0)

        fadescreen(FADE_TO_BLACK)
        delay(16)
        playse(SE_EXIT)
        removeobject(OBJ_RIVAL)
        waitse
        delay(16)
        setflag(FLAG_HIDE_TRAINER_SCHOOL_GEO_RIVAL)
        fadescreen(FADE_FROM_BLACK)

        setflag(FLAG_HIDE_DOLOMITE_CITY_GYM_GUIDE)
    } else {

    }
    
    releaseall
}

movement DolomiteCity_TrainerSchool_Movement_RivalApproachPlayer {
    walk_left
    walk_up 
}

movement DolomiteCity_TrainerSchool_Movement_RivalLeave {
    walk_left
    walk_down * 5
}


text DolomiteCity_TrainerSchool_Geo_Text1 {  
    format("Hi there!\nWelcome to the Pokémon Trainer's School.")  
}  

text DolomiteCity_TrainerSchool_Geo_Text2 {  
    format("I'm Geo, the Gym Leader here.\p"  
           "Right now, I'm teaching these students about Pokémon battles and why type matchups are super important.")  
}  

text DolomiteCity_TrainerSchool_Geo_Text3 {  
    format("But I bet you're not here for a lesson, just like Challenger {RIVAL} over there.\p"  
           "You want a Gym Challenge, don't you?")  
}  

text DolomiteCity_TrainerSchool_Geo_Text_Accept {  
    format("You're excited to prove yourself, huh? That's great!\p"  
           "But before you can battle me, you need to show me your skills in a battle.\p"  
           "Challenger {RIVAL} has been learning a lot from my lessons.\p"
           "Why don't you two have a battle and show me what you've got?")  
}  

text DolomiteCity_TrainerSchool_Geo_Text_RivalIntro {  
    format("Challenger {RIVAL}, come on up.\nShow me what you've learned.")  
}  

text DolomiteCity_TrainerSchool_Geo_Text_AfterBattle {  
    format("Wow, that was impressive, Challenger {PLAYER}.\nYou did really well in that battle.\p"  
           "Now, come meet me at the Dolomite Gym.\nLet's see if you have what it takes to earn my badge.")  
}  

text DolomiteCity_TrainerSchool_Rival_Text1_Caden {  
    format("Oh, you're in for it now, {PLAYER}.\p"  
           "I've been paying attention, and I've got a few new tricks up my sleeve!")  
}  

text DolomiteCity_TrainerSchool_Rival_Text1_Holly {  
    format("This is perfect, {PLAYER}!\p"  
           "A real battle in front of a Gym Leader?\nLet's give it everything we've got!")  
}  

text DolomiteCity_TrainerSchool_CadenDefeated {  
    format("What?! No way!\p"  
           "Ugh, alright, alright. You got me this time.")  
}  

text DolomiteCity_TrainerSchool_HollyDefeated {  
    format("Wow, {PLAYER}, you're a real natural!\p"
           "I've got a lot to learn.")  
}  


script DolomiteCity_TrainerSchool_EventScript_Teacher {
    lock
    faceplayer
    
    if(!flag(FLAG_RECEIVED_QUICK_CLAW))
    {
        msgbox(DolomiteCity_TrainerSchool_Text_Teacher1, MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO) {
            msgbox(DolomiteCity_TrainerSchool_Text_Teacher2)
        } else {
            msgbox(DolomiteCity_TrainerSchool_Text_Teacher3)
            dynmultichoice(20, 4, TRUE, 3, 0, DYN_MULTICHOICE_CB_NONE,
        "Fairy", "Ground", "Electric")

            if (var(VAR_RESULT) != 2) {
                playse(SE_BOO)
                msgbox(DolomiteCity_TrainerSchool_Text_Teacher_ThatsWrong)
            } else {
                playse(SE_DING_DONG)
                msgbox(DolomiteCity_TrainerSchool_Text_Teacher4)
                dynmultichoice(20, 4, TRUE, 3, 0, DYN_MULTICHOICE_CB_NONE,
        "Rock", "Flying", "Steel")
                if (var(VAR_RESULT) != 1) {
                    playse(SE_BOO)
                    msgbox(DolomiteCity_TrainerSchool_Text_Teacher_ThatsWrong)
                } else {
                    playse(SE_DING_DONG)
                    msgbox(DolomiteCity_TrainerSchool_Text_Teacher5)
                    dynmultichoice(20, 4, TRUE, 3, 0, DYN_MULTICHOICE_CB_NONE,
        "Steel", "Poison", "Ghost")
                    if (var(VAR_RESULT) != 0) {
                        playse(SE_BOO)
                        msgbox(DolomiteCity_TrainerSchool_Text_Teacher_ThatsWrong)
                    } else {
                        playse(SE_DING_DONG)
                        msgbox(DolomiteCity_TrainerSchool_Text_Teacher_AllCorrect)
                        giveitem(ITEM_QUICK_CLAW)
                        setflag(FLAG_RECEIVED_QUICK_CLAW)
                    }
                }
            }
        }
    }
    else
    {
        msgbox(DolomiteCity_TrainerSchool_Text_Teacher_FeelFreeToStudy)
    }
    
    release
}

text DolomiteCity_TrainerSchool_Text_Teacher1 {
    format("I'm the teacher here at the Trainer's School.\p"
           "I'm going to ask you a few questions about type matchups.\p"
           "Are you ready?")
}

text DolomiteCity_TrainerSchool_Text_Teacher2 {
    format("No worries! I'll be here if you want to try again.")
}

text DolomiteCity_TrainerSchool_Text_Teacher_ThatsWrong {
    format("That's not quite right. You should study up on your type matchups and try again.")
}

text DolomiteCity_TrainerSchool_Text_Teacher3 {
    format("Great! Here's the first question:\p"
           "What type is super effective against Water-type Pokémon?")
}

text DolomiteCity_TrainerSchool_Text_Teacher4 {
    format("That's correct!\p"
           "Here's the next question:\p"
           "What type is weak to Electric-type moves?")
}

text DolomiteCity_TrainerSchool_Text_Teacher5 {
    format("That's correct!\p"
           "Here's the final question:\p"
           "What type is immune to Poison-type moves?")
}

text DolomiteCity_TrainerSchool_Text_Teacher_AllCorrect {
    format("Wow! You got all the questions right!\p"
           "Here, take this held item as a reward for your participation.")
}

text DolomiteCity_TrainerSchool_Text_Teacher_FeelFreeToStudy {
    format("Feel free to study up as much as you like.")
}


script DolomiteCity_TrainerSchool_EventScript_UnderstandingStats {
    lock
    faceplayer

    msgbox(format("Every Pokémon has six stats: HP, Attack, Defense, Special Attack, Special Defense, and Speed.\p"
           "Speed decides who moves first in battle.\p"
           "Attack and Special Attack deal damage, but only one matters depending on the move!\p"
           "Use the right stat or you're just flailing."))
    release
}

script DolomiteCity_TrainerSchool_EventScript_HowToCatch {
    lock
    faceplayer

    msgbox(format("Poison, Burn, Paralysis, Sleep, and Freeze--they're all super annoying and super useful!\p"
           "Burn lowers Attack. Paralysis sometimes stops Pokémon from moving.\p"
           "Sleep and Freeze are the best since they completely stop Pokémon from moving, but they wear off after a while.\p"
           "You can use them to catch Pokémon easier, but be careful!\p"
           "If you knock them out, you won't be able to catch them at all!"))
    release
}

script DolomiteCity_TrainerSchool_EventScript_HeldItems {
    lock
    faceplayer

    msgbox(format("Give a Pokémon an item and they'll use it in battle by themselves.\p"
           "Like, Leftovers heals them a bit each turn!\p"
           "Or a Berry can heal their HP or cure a status condition.\p"
           "Some items only work once, others boost stats--it's like giving them a secret weapon!\p"
           "Oh, but they can't use man-made items like Potions or Revives.\p"
           "Those are for you to use, not them."))
    release
}

script DolomiteCity_TrainerSchool_EventScript_Abilities {
    lock
    faceplayer

    msgbox(format("Abilities are like a Pokémon's secret superpower.\p"
           "Some boost stats, some change the weather, and some even stop moves from working!\p"
           "Like, if you have a Pokémon with Levitate, they can't be hit by Ground-type moves.\p"
           "It's like having a built-in shield!"))
    release
}