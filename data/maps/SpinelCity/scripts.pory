//VAR_SPINEL_CITY_STATE
//0: Haven't beaten Melody
//1: Jasmine pep talk
//2 Go to Quarry

const OBJ_JASMINE = 1

mapscripts SpinelCity_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SPINEL_CITY_STATE, 1: SpinelCity_JasmineReassurance
    ]
}

script SpinelCity_JasmineReassurance {
    lockall

    playse(SE_PIN)
    applymovement(OBJ_JASMINE, Common_Movement_ExclamationMark)
    waitse
    waitmovement(0)
    delay(32)
    applymovement(OBJ_JASMINE, Common_Movement_WalkInPlaceFasterRight)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
    
    speakername("Jasmine")
    msgbox(SpinelCity_Text_JasmineReassurance1, MSGBOX_YESNO)

    while(var(VAR_RESULT) == NO)
    {
        msgbox(SpinelCity_Text_JasmineForceYes, MSGBOX_YESNO)
    }

    msgbox(SpinelCity_Text_JasmineSaysThankYou)

    setvar(VAR_SPINEL_CITY_STATE, 2)
    setflag(FLAG_HIDE_ROUTE_304_ETERNITY_GRUNT)
    clearflag(FLAG_HIDE_ZIRCON_QUARRY_ETERNITY_GRUNTS)

    releaseall
}

text SpinelCity_Text_JasmineReassurance1 {
    format("Oh... {PLAYER}, there you are.\p"
           "Melody just ran off crying... so, I guess... you won?\p"
           "She tries so hard, and when she loses... it really hurts her.\p"
           "I know what that feels like... I used to struggle with it too.\p"
           "She usually goes to the quarry when she's upset. It's quiet there.\p"
           "Would... would you go check on her?")
}

text SpinelCity_Text_JasmineSaysThankYou {
    format("Thank you...\p"
           "She may act cheerful, but she's sensitive underneath.\p"
           "If anyone can reach her right now... it's probably you.")
}

text SpinelCity_Text_JasmineForceYes {
    format("I... I really think you should.\p"
           "She looks up to you... more than she says.\p"
           "Please, {PLAYER}...\p"
           "She needs someone right now.")
}

script SpinelCity_EventScript_Jasmine {
    lock
    facePLAYER

    speakername("Jasmine")
    msgbox(SpinelCity_Text_JasmineSaysThankYou)

    release
}

