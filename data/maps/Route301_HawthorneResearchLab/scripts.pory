const OBJ_PROF_HAWTHORNE = 1
const OBJ_RIVAL = 2
const OBJ_CHESPIN = 3
const OBJ_FENNEKIN = 4
const OBJ_FROAKIE = 5

//VAR_HAWTHORNE_LAB_STATE
//0: Walk in
//1: Choose starter
//2: Starter chosen
//3: Delivered parcel
//4: Adventure Started

mapscripts Route301_HawthorneResearchLab_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
    }
    
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_HAWTHORNE_LAB_STATE, 0: Route301_HawthorneResearchLab_MapScript_MeetProf
        VAR_HAWTHORNE_LAB_STATE, 3: Route301_HawthorneResearchLab_MapScript_ReportBack
    ]
}

script Route301_HawthorneResearchLab_MapScript_ReportBack {
    lockall

    applymovement(OBJ_EVENT_ID_PLAYER, Route301_HawthorneResearchLab_Movement_Player_WalkIntoLab)
    waitmovement(0)
    applymovement(OBJ_PROF_HAWTHORNE, Common_Movement_WalkInPlaceFasterDown)

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne1)
    closemessage

    fadescreen(FADE_TO_BLACK)
    delay(16)
    fadescreen(FADE_FROM_BLACK)

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne2)
    closemessage

    fadescreen(FADE_TO_BLACK)
    delay(16)
    speakername("{RIVAL}")
    msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Rival1)
    closemessage

    addobject(OBJ_RIVAL)
    setobjectxy(OBJ_RIVAL, 5, 6)
    fadescreen(FADE_FROM_BLACK)

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne3)
    playbgm(MUS_ROUTE122, TRUE)
    applymovement(OBJ_PROF_HAWTHORNE, Common_Movement_WalkInPlaceFasterDown)
    msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne4)
    closemessage
    
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox(format("You obtained the Pokédex!"))
    waitfanfare

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne5)
    closemessage

    giveitem(ITEM_POKE_BALL, 10)

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne6)

    speakername("{RIVAL}")
    checkplayergender
    if(var(VAR_RESULT) == FEMALE)
    {
        msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Caden1)
    }
    else
    {
        msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Holly1)
    }

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne7)

    speakername("{RIVAL}")
    if(var(VAR_RESULT) == FEMALE)
    {
        msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Caden2)
    }
    else
    {
        msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Holly2)
    }

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne8)
    closemessage

    fadedefaultbgm
    
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
    applymovement(OBJ_RIVAL, Route301_HawthorneResearchLab_Movement_Rival_RunOutOfLab)
    waitmovement(0)
    fadedefaultbgm
    playse(SE_EXIT)
    removeobject(OBJ_RIVAL)
    waitse
    
    setvar(VAR_HAWTHORNE_LAB_STATE, 4)
    setvar(VAR_ROUTE301_STATE, 4)
    setflag(FLAG_HIDE_ROUTE_301_RIVAL)
    setflag(FLAG_SYS_POKEDEX_GET)
    clearflag(FLAG_HIDE_ROUTE_301_MOM_OUTSIDE)
    special(SetUnlockedPokedexFlags)

    releaseall

}

movement Route301_HawthorneResearchLab_Movement_Rival_RunOutOfLab {
    walk_fast_down * 6
}

text Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne1 {
    format("Ah, {PLAYER}! Welcome back!\nHow did the delivery go?")
}

text Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne2 {
    format("Excellent work! I knew I could count on you.\p"
            "The Mega Evolution Guru will be able to put those stones to good use.\p"
            "It may seem like a simple errand, but every great journey starts with small steps!\p"
            "Now then, let's wait for {RIVAL} to return and we can get started on your next assignment.")
}

text Route301_HawthorneResearchLab_Text_ReportBack_Rival1 {
    format("Professor, I'm back!\nFieldwork is way more exhausting than I expected.")
}

text Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne3 {
    format("Ah, just in time, {RIVAL}! You and {PLAYER} have both done a fine job with your first tasks.\p"
            "But I have one more important request for you both.")
}

text Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne4 {
    format("The world of Pokémon is vast, and there's still so much we don't know.\p"
            "As you already know, my research focuses on understanding Pokémon habitats, behaviors, and how they grow alongside humans.\p"
            "But no single researcher can explore every corner of the region alone.\p"
            "This is where you two come in.\nI'd like you both to help me complete the Avaro Region's Pokédex.\p"
            "By traveling, battling, and capturing Pokémon, you'll be contributing valuable data to Pokémon research!\p"
            "And who knows? You might even discover something new about Pokémon in the process.\p"
            "Here, you'll need this.")
}

text Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne5 {
    format("This fancy gadget is called a Pokédex.\p"
            "It's a high-tech device that records data on any Pokémon you encounter or catch.\p"
            "It's a valuable tool for any Trainer, and I'm entrusting you both with it.\p"
            "Oh, and before I forget--here, take these as well.")
}

text Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne6 {
    format("These'll help you catch Pokémon out in the wild.\p"
            "The more Pokémon you catch, the more we'll learn about their behaviors and habitats.\p"
            "If you run out them, you can always buy more at Poké Marts.")
}

text Route301_HawthorneResearchLab_Text_ReportBack_Caden1 {
    format("Oh, now this is what I'm talking about!\pExploring the entire region, catching tons of Pokémon--I'm in!")   
}

text Route301_HawthorneResearchLab_Text_ReportBack_Holly1 {
    format("That sounds amazing!\pI've always wanted to see more of the world, and this is the perfect excuse!")
}

text Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne7 {  
    format("I'm glad to hear you're both excited!\p"  
           "Of course, traveling the region isn't just about research--it's about making memories, meeting new people, and growing as Trainers.\p"  
           "Perhaps you two should also consider taking on the Pokémon League.\p"  
           "The League challenge is a test of skill, strategy, and perseverance.\p"  
           "If you collect all eight Gym Badges, you'll earn the right to compete in the Paragon Conference, challenge the Elite Four--and perhaps even take on the Champion!\p"  
           "Many of the world's greatest Trainers began as Pokédex researchers.\p"  
           "After all, battling and studying Pokémon go hand in hand!")  
}  

text Route301_HawthorneResearchLab_Text_ReportBack_Caden2 {  
    format("The Pokémon League, huh?\p"  
           "That actually sounds kinda awesome... \n"  
           "I mean, if we're already traveling, why not prove we're the best along the way?")  
}  

text Route301_HawthorneResearchLab_Text_ReportBack_Holly2 {  
    format("Taking on the League sounds like a huge challenge...\p"  
           "But honestly? I think it could be really fun!\p"  
           "I'd love to see just how strong I can become.")  
}  

text Route301_HawthorneResearchLab_Text_ReportBack_Hawthorne8 {  
    format("That's the spirit!\p"  
           "Whatever path you choose, I have no doubt you'll both grow into remarkable Trainers.\p"
           "The first Gym is in Dolomite City. I suggest you start there.")
}  




script Route301_HawthorneResearchLab_MapScript_MeetProf {
    lockall

    addobject(OBJ_RIVAL)
    addobject(OBJ_CHESPIN)
    addobject(OBJ_FENNEKIN)
    addobject(OBJ_FROAKIE)

    applymovement(OBJ_RIVAL, Route301_HawthorneResearchLab_Movement_Rival_WalkIntoLab)
    applymovement(OBJ_EVENT_ID_PLAYER, Route301_HawthorneResearchLab_Movement_Player_WalkIntoLab)
    waitmovement(0)

    speakername("???")
    msgbox(Route301_HawthorneResearchLab_Text_MeetProf_ProfHawthorne1)
    
    speakername("{RIVAL}")
    checkplayergender
    if(var(VAR_RESULT) == FEMALE)
    {
        msgbox(Route301_HawthorneResearchLab_Text_MeetProf_Caden1)
    }
    else
    {
        msgbox(Route301_HawthorneResearchLab_Text_MeetProf_Holly1)
    }

    dynmultichoice(20, 8, TRUE, 2, 0, DYN_MULTICHOICE_CB_NONE,
    "Nice to meet you!", "Sorry for intruding!")
    
    speakername("???")
    if (var(VAR_RESULT) == 0) {
        msgbox(format("Well, it's nice to meet you too!"))
    } else {
        msgbox(format("Oh, don't worry about it!\nI'm always happy to meet new people."))
    }
    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_MeetProf_ProfHawthorne2)

    applymovement(OBJ_PROF_HAWTHORNE, Common_Movement_WalkInPlaceFasterUp)
    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_MeetProf_ProfHawthorne3)
    closemessage

    call(Route301_HawthorneResearchLab_MapScript_StarterIntro)

    applymovement(OBJ_PROF_HAWTHORNE, Common_Movement_WalkInPlaceFasterDown)

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_MeetProf_ProfHawthorne4)

    setvar(VAR_HAWTHORNE_LAB_STATE, 1)
    setobjectmovementtype(OBJ_CHESPIN, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype(OBJ_FENNEKIN, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype(OBJ_FROAKIE, MOVEMENT_TYPE_FACE_DOWN)

    releaseall
}

script Route301_HawthorneResearchLab_MapScript_StarterIntro {
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, Route301_HawthorneResearchLab_Movement_CameraPan1)
    waitmovement(OBJ_EVENT_ID_CAMERA)

    playmoncry(SPECIES_CHESPIN, CRY_MODE_NORMAL)
    applymovement(OBJ_CHESPIN, Route301_HawthorneResearchLab_Movement_StarterHop)
    waitmoncry
    waitmovement(0)

    speakername("Hawthorne")
    msgbox(format("Chespin, the Spiny Nut Pokémon."))
    closemessage

    applymovement(OBJ_EVENT_ID_CAMERA, Route301_HawthorneResearchLab_Movement_CameraPan2)

    playmoncry(SPECIES_FENNEKIN,CRY_MODE_NORMAL)
    applymovement(OBJ_FENNEKIN, Route301_HawthorneResearchLab_Movement_StarterHop)
    waitmoncry
    waitmovement(0)

    speakername("Hawthorne")
    msgbox(format("Fennekin, the Fox Pokémon."))
    closemessage

    applymovement(OBJ_EVENT_ID_CAMERA, Route301_HawthorneResearchLab_Movement_CameraPan2)

    playmoncry(SPECIES_FROAKIE, CRY_MODE_NORMAL)
    applymovement(OBJ_FROAKIE, Route301_HawthorneResearchLab_Movement_StarterHop)
    waitmoncry
    waitmovement(0)

    speakername("Hawthorne")
    msgbox(format("And last but not least,\nFroakie, the Bubble Frog Pokémon."))
    closemessage

    applymovement(OBJ_EVENT_ID_CAMERA, Route301_HawthorneResearchLab_Movement_CameraPan3)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    special(RemoveCameraObject)
}

script Route301_HawthorneResearchLab_Trigger_WhereAreYouGoing {
    lock
    applymovement(OBJ_PROF_HAWTHORNE, Common_Movement_FacePlayer)
    waitmovement(0)

    speakername("Hawthorne")
    msgbox(format("Where are you going?\nDon't you want to choose a Pokémon?"))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
    waitmovement(0)
    release
}

script Route301_HawthorneResearchLab_EventScript_ReceiveStarter {
    bufferleadmonspeciesname(STR_VAR_1)
    playfanfare(MUS_EVOLVED)
    msgbox(format("You chose {STR_VAR_1}!"))
    waitfanfare
    msgbox(format("Would you like to give {STR_VAR_1} a nickname?"), MSGBOX_YESNO)
    closemessage    

    if (var(VAR_RESULT) == 1) {
        setvar(VAR_0x8004, 0)
        call(Common_EventScript_NameReceivedPartyMon)
    }

    switch(var(VAR_STARTER_MON)) {
        case 0:
           bufferspeciesname(STR_VAR_2, SPECIES_FROAKIE)
        case 1:
            bufferspeciesname(STR_VAR_2, SPECIES_CHESPIN)
        case 2:
            bufferspeciesname(STR_VAR_2, SPECIES_FENNEKIN)
    }

    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(0)

    speakername("{RIVAL}")
    checkplayergender
    if(var(VAR_RESULT) == FEMALE)
    {
        msgbox(Route301_HawthorneResearchLab_Text_ReceiveStarter_Caden1)
    }
    else
    {
        msgbox(Route301_HawthorneResearchLab_Text_ReceiveStarter_Holly1)
    }
    closemessage

    switch(var(VAR_STARTER_MON)) {
        case 0:
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
            applymovement(OBJ_RIVAL, Route301_HawthorneResearchLab_Movement_ReceiveStarter_RivalWalkToFroakie)
        case 1:
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
            applymovement(OBJ_RIVAL, Route301_HawthorneResearchLab_Movement_ReceiveStarter_RivalWalkToChespin)
        case 2:
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
            applymovement(OBJ_RIVAL, Route301_HawthorneResearchLab_Movement_ReceiveStarter_RivalWalkToFennekin)
    }
    waitmovement(0)

    if(var(VAR_RESULT) == FEMALE)
    {
        msgbox(Route301_HawthorneResearchLab_Text_ReceiveStarter_Caden2)
    }
    else
    {
        msgbox(Route301_HawthorneResearchLab_Text_ReceiveStarter_Holly2)
    }

    switch(var(VAR_STARTER_MON)) {
        case 0:
            playmoncry(SPECIES_FROAKIE, CRY_MODE_NORMAL)
            applymovement(OBJ_FROAKIE, Route301_HawthorneResearchLab_Movement_StarterHop)

            speakername("Froakie")
            msgbox("(Froakie croaks confidently!)")
            closemessage
            waitmovement(0)
            removeobject(OBJ_FROAKIE)
            break

        case 1:
            playmoncry(SPECIES_CHESPIN, CRY_MODE_NORMAL)
            applymovement(OBJ_CHESPIN, Route301_HawthorneResearchLab_Movement_StarterHop)

            speakername("Chespin")
            msgbox("(Chespin chirps happily!)")
            closemessage
            waitmovement(0)
            removeobject(OBJ_CHESPIN)
            break

        case 2:
            playmoncry(SPECIES_FENNEKIN, CRY_MODE_NORMAL)
            applymovement(OBJ_FENNEKIN, Route301_HawthorneResearchLab_Movement_StarterHop)

            speakername("Fennekin")
            msgbox("(Fennekin lets out a cheerful bark!)")
            closemessage
            waitmovement(0)
            removeobject(OBJ_FENNEKIN)
            break
    }

    applymovement(OBJ_PROF_HAWTHORNE, Common_Movement_WalkInPlaceFasterUp)
    applymovement(OBJ_RIVAL, Common_Movement_WalkInPlaceFasterDown)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(0)

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ReceiveStarter_Hawthorne1)

    setflag(FLAG_SYS_POKEMON_GET)
    call(Route301_HawthorneResearchLab_EventScript_ExplainErrand)
}

text Route301_HawthorneResearchLab_Text_ReceiveStarter_Caden1 {
    format("Oh, {STR_VAR_1}, huh? Yeah, I can see it.\p"
           "You two look like a good match.")
}

text Route301_HawthorneResearchLab_Text_ReceiveStarter_Caden2 {
    format( "You know what? A challenge is good! Makes things more exciting, right?"
            "Alright, then... I'll pick {STR_VAR_2}\p"
           "I can't wait to see how we both grow together!")
}

text Route301_HawthorneResearchLab_Text_ReceiveStarter_Holly1 {
    format("Wow, {PLAYER}! Nice choice!\p"
           "I think {STR_VAR_1} suits you perfectly.")
}

text Route301_HawthorneResearchLab_Text_ReceiveStarter_Holly2 {
    format("Hmm, I think I'll go with {STR_VAR_2}!\nIt looks so strong--and cute, too!\p"
           "I can't wait to see how we both grow together!")
}

text Route301_HawthorneResearchLab_Text_ReceiveStarter_Hawthorne1 {
    format("Excellent choices!\nThese Pokémon will be your partners through thick and thin.\p"
           "Treat them well, and they'll be sure to grow alongside you.")
}

movement Route301_HawthorneResearchLab_Movement_ReceiveStarter_RivalWalkToChespin {
    walk_up * 3
    walk_in_place_faster_up
}

movement Route301_HawthorneResearchLab_Movement_ReceiveStarter_RivalWalkToFennekin {
    walk_up * 3
    walk_right
    walk_in_place_faster_up
}

movement Route301_HawthorneResearchLab_Movement_ReceiveStarter_RivalWalkToFroakie {
    walk_right * 2
    walk_up * 3
    walk_in_place_faster_up
}

script Route301_HawthorneResearchLab_EventScript_ExplainErrand {
    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ExplainErrand_Hawthorne1)
    closemessage

    applymovement(OBJ_RIVAL, Common_Movement_WalkInPlaceFasterDown)
    speakername("{RIVAL}")
    checkplayergender
    if(var(VAR_RESULT) == FEMALE)
    {
        msgbox(Route301_HawthorneResearchLab_Text_ExplainErrand_Caden1)
    }
    else
    {
        msgbox(Route301_HawthorneResearchLab_Text_ExplainErrand_Holly1)
    }

    speakername("Hawthorne")
    msgbox(Route301_HawthorneResearchLab_Text_ExplainErrand_Hawthorne2)
    closemessage

    giveitem(ITEM_PARCEL)

    fadescreen(FADE_TO_BLACK)
    setvar(VAR_HAWTHORNE_LAB_STATE, 2)
    setvar(VAR_ROUTE301_STATE, 1)
    setflag(FLAG_CHOSE_STARTER)
    removeobject(OBJ_CHESPIN)
    removeobject(OBJ_FENNEKIN)
    removeobject(OBJ_FROAKIE)
    fadescreen(FADE_FROM_BLACK)
}

text Route301_HawthorneResearchLab_Text_ExplainErrand_Hawthorne1 {
    format("Now that you've both chosen your Pokémon, I have a favor to ask of you two.\p"
           "{RIVAL}, I need this package delivered to the Pokémon Trainer School in Dolomite City.\p"
           "It contains some very important materials, and I trust you to handle it carefully.\p"
           "As for you, {PLAYER}, I could use some help cataloging Pokémon habitats on Route 301.\p"
           "Think of it as a chance to get to know your new partner better!")
}

text Route301_HawthorneResearchLab_Text_ExplainErrand_Caden1 {
    format( "Wait, hang on--why don't I do the fieldwork and let {PLAYER} deliver the package?\p"
            "You're the one who always says I need more experience in the field!")
}

text Route301_HawthorneResearchLab_Text_ExplainErrand_Holly1 {
    format( "Oh, Professor, wouldn't it make more sense if I stayed to help with the fieldwork?\p"
            "You always say it's important to study Pokémon up close!")
}

text Route301_HawthorneResearchLab_Text_ExplainErrand_Hawthorne2 {
    format( "Hahaha! Fair enough.\nLet's switch things around.\p"
            "{RIVAL}, you'll stay here for fieldwork. And you, {PLAYER}, can deliver the package to Dolomite City's Trainer School.\p"
            "Once you're both done, come back here and we'll get started on your second research assignment.\p"
            "Oh, but before you go, don't forget to tell your parents where you're headed!\p"
            "I don't want to be the one to get a call from them asking where you've run off to... again!")
}

script Route301_HawthorneResearchLab_EventScript_Chespin {
    lock

    showmonpic(SPECIES_CHESPIN, 10, 3)
    playmoncry(SPECIES_CHESPIN, CRY_MODE_NORMAL)

    speakername("Chespin")
    msgbox(format("(Chespin chirps excitedly!)"))

    speakername("Hawthorne")
    msgbox(format("Chespin is a Grass-type Pokémon.\p"
                  "It has a tough shell and loves to play rough.\p"
                  "Would you like to choose the Grass-type Pokémon, Chespin?"), MSGBOX_YESNO)

    hidemonpic

    if(var(VAR_RESULT) == YES) {
        playmoncry(SPECIES_CHESPIN, CRY_MODE_NORMAL)
        applymovement(OBJ_CHESPIN, Route301_HawthorneResearchLab_Movement_StarterHop)

        speakername("Chespin")
        msgbox("(Chespin chirps happily!)")
        closemessage
        waitmovement(0)

        givemon(SPECIES_CHESPIN, 5)
        setvar(VAR_STARTER_MON, 0)

        removeobject(OBJ_CHESPIN)
        call(Route301_HawthorneResearchLab_EventScript_ReceiveStarter)
    }
    releaseall
}

script Route301_HawthorneResearchLab_EventScript_Fennekin {
    lock

    showmonpic(SPECIES_FENNEKIN, 10, 3)
    playmoncry(SPECIES_FENNEKIN, CRY_MODE_NORMAL)

    speakername("Fennekin")
    msgbox(format("(Fennekin tilts its head curiously.)"))

    speakername("Hawthorne")
    msgbox(format("Fennekin is a Fire-type Pokémon.\p"
                  "It loves to snack on twigs and has a fiery personality.\p"
                  "Would you like to choose the Fire-type Pokémon, Fennekin?"), MSGBOX_YESNO)

    hidemonpic

    if(var(VAR_RESULT) == YES) {
        playmoncry(SPECIES_FENNEKIN, CRY_MODE_NORMAL)
        applymovement(OBJ_FENNEKIN, Route301_HawthorneResearchLab_Movement_StarterHop)

        speakername("Fennekin")
        msgbox(format("(Fennekin lets out a cheerful bark!)"))
        closemessage
        waitmovement(0)

        givemon(SPECIES_FENNEKIN, 5)
        setvar(VAR_STARTER_MON, 1)

        removeobject(OBJ_FENNEKIN)
        call(Route301_HawthorneResearchLab_EventScript_ReceiveStarter)
    }
    releaseall
}

script Route301_HawthorneResearchLab_EventScript_Froakie {
    lock

    showmonpic(SPECIES_FROAKIE, 10, 3)
    playmoncry(SPECIES_FROAKIE, CRY_MODE_NORMAL)

    speakername("Froakie")
    msgbox(format("(Froakie looks at you with determination.)"))

    speakername("Hawthorne")
    msgbox(format("Froakie is a Water-type Pokémon.\p"
                  "It uses its bubbles to protect itself and its Trainer.\p"
                  "Would you like to choose the Water-type Pokémon, Froakie?"), MSGBOX_YESNO)

    hidemonpic

    if(var(VAR_RESULT) == YES) {
        playmoncry(SPECIES_FROAKIE, CRY_MODE_NORMAL)
        applymovement(OBJ_FROAKIE, Route301_HawthorneResearchLab_Movement_StarterHop)

        speakername("Froakie")
        msgbox(format("(Froakie croaks confidently!)"))
        closemessage
        waitmovement(0)

        givemon(SPECIES_FROAKIE, 5)
        setvar(VAR_STARTER_MON, 2)

        removeobject(OBJ_FROAKIE)
        call(Route301_HawthorneResearchLab_EventScript_ReceiveStarter)
    }
    releaseall
}

movement Route301_HawthorneResearchLab_Movement_Rival_WalkIntoLab {
    walk_up * 5
    walk_left
    walk_in_place_up
}

movement Route301_HawthorneResearchLab_Movement_Player_WalkIntoLab {
    walk_up * 6
}

text Route301_HawthorneResearchLab_Text_MeetProf_ProfHawthorne1 {
    format("Ah, {RIVAL}! Right on time, as usual.\nI was just about to--oh?\lWho's this with you?")
}

text Route301_HawthorneResearchLab_Text_MeetProf_ProfHawthorne2 {
    format("Welcome to my Research Lab, {PLAYER}!\nI'm Professor Hawthorne, and I study Pokémon habitats and how they interact with human communities here in Avaro.")
}

text Route301_HawthorneResearchLab_Text_MeetProf_ProfHawthorne3 {
    format("You've come at the perfect time.\nI was just about to let {RIVAL} choose their first Pokémon.\p"
           "{PLAYER}, since you're here, why don't you take one too?\p"
           "A Trainer's journey is always better with a partner by your side.")
}

text Route301_HawthorneResearchLab_Text_MeetProf_ProfHawthorne4 {
    format("Each one is a unique species with its own strengths and weaknesses.\p"
           "Take your time and choose the one that speaks to you the most:\p"
           "Chespin, the Grass-type Pokémon.\n"
           "Fennekin, the Fire-type Pokémon.\n"
           "Froakie, the Water-type Pokémon.")
}

text Route301_HawthorneResearchLab_Text_MeetProf_Caden1 {
   format("This is my new neighbor, {PLAYER}--the one I told you moved in yesterday.\p"
          "Thought I'd bring her along to see the lab.")
}

text Route301_HawthorneResearchLab_Text_MeetProf_Holly1 {
   format("Oh, this is my new neighbor, {PLAYER}! He just moved here from Hoenn.\p"
          "I thought you wouldn't mind if they tagged along.")
}

movement Route301_HawthorneResearchLab_Movement_CameraPan1 {
    walk_fast_left
    walk_fast_up * 4
}

movement Route301_HawthorneResearchLab_Movement_CameraPan2 {
    walk_slow_right
}

movement Route301_HawthorneResearchLab_Movement_CameraPan3 {
    walk_fast_down * 4
    walk_fast_left
}

movement Route301_HawthorneResearchLab_Movement_StarterHop {
    jump_in_place_up_down
    delay_16 * 2
}