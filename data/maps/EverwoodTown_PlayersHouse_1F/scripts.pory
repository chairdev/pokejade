const OBJ_MOM = 1

mapscripts EverwoodTown_PlayersHouse_1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_EVERWOOD_TOWN_STATE, 0: EverwoodTown_PlayersHouse_1F_MapScript_MeetMom
    ]
}



script EverwoodTown_PlayersHouse_1F_MapScript_MeetMom {
    lockall
    playbgm(MUS_FOLLOW_ME, 0)
    applymovement(OBJ_MOM, MomApproachPlayer1)
    waitmovement(0)
    
    msgbox(MeetMomText1)
    fadedefaultbgm
    applymovement(OBJ_MOM, MomMoveBack)
    waitmovement(0)

    setvar(VAR_EVERWOOD_TOWN_STATE, 1)
    
    
    releaseall
}

text MeetMomText1 {
    format("Oh! Good morning, {PLAYER}!\p"
           "You sure are up early today.\p"
           "I guess you're just as excited as I am!\p"
           "Today is the day you become a Pokémon Trainer!\p"
           "You'll get your first Pokémon from Professor Sanzashi at the Research Lab.\p"
           "I can't wait to see which one you choose!\p"
           "I won't keep you any longer. I'm sure Professor Sanzashi is waiting for you!"
           )
              
}

movement MomApproachPlayer1 {
    walk_up
    walk_right * 2
    face_up
}

movement MomMoveBack {
    walk_left * 2
    walk_down
    face_right
}

script EverwoodTown_PlayersHouse_1F_EventScript_Mom {
    lockall
    faceplayer

    checkplayergender()
    if(var(VAR_RESULT) ==  MALE)
    {
        msgbox(RivalCameByText1A)
    }
    else
    {
        msgbox(RivalCameByText1B)
    }

    releaseall
}

text RivalCameByText1A {
    format("Our neighbor {RIVAL} came looking for you earlier.\p"
           "I told her you were still asleep.\p"
           "...I wonder what she wanted?"
           )
}

text RivalCameByText1B {
    format("Our neighbor {RIVAL} came looking for you earlier.\p"
           "I told him you were still asleep.\p"
           "...I wonder what he wanted?"
           )
}

script EverwoodTown_PlayersHouse_1F_EventScript_Delcatty {
    lock
    faceplayer
    playmoncry(SPECIES_DELCATTY, CRY_MODE_NORMAL)
    msgbox(format("Mreeeow!"))
    waitmoncry
    release
}

