const OBJ_MOM = 1

mapscripts EverwoodTown_PlayersHouse_1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_EVERWOOD_TOWN_STATE, 0: EverwoodTown_PlayersHouse_1F_MapScript_MeetMom
    ]
}



script EverwoodTown_PlayersHouse_1F_MapScript_MeetMom {
    lockall
    setrespawn(HEAL_LOCATION_EVERWOOD_TOWN)
    playbgm(MUS_FOLLOW_ME, 0)
    applymovement(OBJ_MOM, MomApproachPlayer1)
    waitmovement(0)
    
    speakername("Mom")
    msgbox(MeetMomText1)
    closemessage
    
    fadedefaultbgm
    applymovement(OBJ_MOM, MomMoveBack)
    waitmovement(0)

    setvar(VAR_EVERWOOD_TOWN_STATE, 1)
    
    
    releaseall
}

text MeetMomText1 {
    format("Oh! Good morning, {PLAYER}!\p"
           "You sure are up early today.\p"
           "I guess you're just as excited as I am!\p"
           "Today is the day you become a Pokémon Trainer!\p"
           "You'll get your first Pokémon from Professor Hawthorne at the Research Lab.\p"
           "I can't wait to see which one you choose!\p"
           "I won't keep you any longer. I'm sure Professor Hawthorne is waiting for you!"
           )
              
}

movement MomApproachPlayer1 {
    walk_up
    walk_right * 2
    walk_in_place_faster_up
}

movement MomMoveBack {
    walk_left * 2
    walk_down
    walk_in_place_faster_right
}

script EverwoodTown_PlayersHouse_1F_EventScript_Mom {
    lockall
    faceplayer

    if(var(VAR_EVERWOOD_TOWN_STATE) < 4)
    {
        checkplayergender()
        if(var(VAR_RESULT) ==  MALE)
        {
            msgbox(RivalCameByText1A)
        }
        else
        {
            msgbox(RivalCameByText1B)
        }
    }
    else
    {
        msgbox(EverwoodTown_PlayersHouse_1F_Text_MomHealsParty)
        closemessage
        call(Common_EventScript_OutOfCenterPartyHeal)
        incrementgamestat(GAME_STAT_RESTED_AT_HOME)
        msgbox(EverwoodTown_PlayersHouse_1F_Text_GoGetEm)
        release
    }
        
    

    releaseall
}

text EverwoodTown_PlayersHouse_1F_Text_MomHealsParty {
    format("Welcome home, {PLAYER}!\p"
           "You look tired.\nWhy don't you rest a bit?")
}

text EverwoodTown_PlayersHouse_1F_Text_GoGetEm { //right after healing
    format("There! You look like you're ready to take on the world!\p"
           "Go get 'em, {PLAYER}!"
           )
}



text RivalCameByText1A {
    format("Our neighbor {RIVAL} came looking for you earlier.\p"
           "I told her you were still asleep.\p"
           "...I wonder what she wanted?"
           )
}

text RivalCameByText1B {
    format("Our neighbor {RIVAL} came looking for you earlier.\p"
           "I told him you were still asleep.\p"
           "...I wonder what he wanted?"
           )
}

script EverwoodTown_PlayersHouse_1F_EventScript_Delcatty {
    lock
    faceplayer
    playmoncry(SPECIES_DELCATTY, CRY_MODE_NORMAL)
    msgbox(format("Mreeeow!"))
    waitmoncry
    release
}

